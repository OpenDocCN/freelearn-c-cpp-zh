<html><head></head><body><div><h1 class="header-title">Customization in Qt Quick</h1>
                
            
            
                
<p>In the previous chapter, you learned how to use controls and layouts provided by Qt Quick to build the user interface of your application. Qt contains numerous QML types that can serve as building blocks for your game, providing rich functionality and a nice appearance. However, sometimes you need to create a custom component that satisfies the needs of your game. In this chapter, we will show a couple of convenient ways to extend your QML project with custom components. By the end of this chapter, you will know how to perform custom painting on a canvas, handle various input events, and implement lazy loading for your components. We will also see how to integrate a C++ object into QML's object tree.</p>
<p class="mce-root">The main topics covered in this chapter are as listed:</p>
<ul>
<li>Creating a custom component</li>
<li>Handling mouse, touch, keyboard, and gamepad events</li>
<li>Dynamic and lazy loading</li>
<li>Painting on Canvas using JavaScript</li>
</ul>


            

            
        
    </div>



  
<div><h1 class="header-title">Creating a custom QML component</h1>
                
            
            
                
<p>We already touched the topic of custom components when we worked with the form editor in the previous chapter. Our QML files implemented reusable components with a clean interface that can be used in the rest of the application. We will now take a more low-level approach and create a new QML component directly from QML code using the basic Qt Quick building blocks. Our component will be a button with a rounded shape and a nice background. The button will hold definable text and an icon. Our component should look good for different texts and icons.</p>


            

            
        
    </div>



  
<div><h1 class="header-title">Time for action – Creating a button component</h1>
                
            
            
                
<p>Start by creating a new project in Qt Creator. Choose Qt Quick Application - Empty as the project template. Name the project <kbd>custom_button</kbd> and leave the rest of the options unchanged.</p>
<p>At this point, you should end up with a QML document containing an empty window. Let's start by creating the button frame. Edit the <kbd>main.qml</kbd> file to add a new <kbd>Rectangle</kbd> item to the window:</p>
<pre>import QtQuick 2.9
import QtQuick.Window 2.2

Window {
    visible: true
    width: 640
    height: 480
    title: qsTr("Hello World")

    Rectangle {
        id: button
        anchors.centerIn: parent
        border { width: 1; color: "black" }
        radius: 5
        width: 100; height: 30
        gradient: Gradient {
            GradientStop { position: 0; color: "#eeeeee" }
            GradientStop { position: 1; color: "#777777" }
        }
    }
}</pre>
<p>After running the project, you should see a result similar to the following:</p>
<div><img src="img/479fb0fc-08a6-4fe3-afdd-0705da063a8d.png" style="width:19.33em;height:10.25em;" width="321" height="169"/></div>


            

            
        
    </div>



  
<div><h1 class="header-title">What just happened?</h1>
                
            
            
                
<p>You can see that the rectangle is centered in the window using a <kbd>centerIn</kbd> anchor binding that we didn't mention before. This is one of the two special anchors that are provided for convenience, to avoid having to write too much code. Using <kbd>centerIn</kbd> is equivalent to setting both <kbd>horizontalCenter</kbd> and <kbd>verticalCenter</kbd>. The other convenience binding is <kbd>fill</kbd>, which makes one item occupy the whole area of another item (similar to setting the left, right, top, and bottom anchors to their respective anchor lines in the destination item).</p>
<p>Instead of setting a solid color for the button, we declared the background to be a linear gradient. We bound a <kbd>Gradient</kbd> element to the <kbd>gradient</kbd> property and defined two <kbd>GradientStop</kbd> elements as its children, where we specified two colors to blend between. <kbd>Gradient</kbd> does not inherit from <kbd>Item</kbd> and thus is not a visual Qt Quick element. Instead, it is just a QML object that serves as a data holder for the gradient definition.</p>
<p>The <kbd>Item</kbd> type has a property called <kbd>children</kbd> that contains a list of the visual children (<kbd>Item</kbd> instances) of an item and another property called <kbd>resources</kbd>, which contains a list of non-visual objects (such as <kbd>Gradient</kbd> or <kbd>GradientStop</kbd>) for an item. Normally, you don't need to use these properties when adding visual or non-visual objects to an item, as the item will automatically assign child objects to appropriate properties. Note that in our code, the <kbd>Gradient</kbd> object is not a child object of the <kbd>Rectangle</kbd>; it is just assigned to its <kbd>gradient</kbd> property.</p>


            

            
        
    </div>



  
<div><h1 class="header-title">Time for action – Adding button content</h1>
                
            
            
                
<p>The next step is to add text and an icon to the button. First, copy the icon file to the project directory. In Qt Creator, locate the <kbd>qml.qrc</kbd> resource file in the project tree. In the context menu of the resource file, select Add Existing Files and select your icon file. The file will be added to the resources and will appear in the project tree. Our example file is called <kbd>edit-undo.png</kbd>, and the corresponding resource URL is <kbd>qrc:/edit-undo.png</kbd>.</p>
<p>You can get the resource path or URL of a file by locating that file in the project tree and using the Copy Path or Copy URL option in its context menu.</p>
<p>Next, we will add the icon and the text to our button using another item type called <kbd>Row</kbd>, as shown:</p>
<pre>Rectangle {
    id: button
    anchors.centerIn: parent
    border { width: 1; color: "black" }
    radius: 5
    gradient: Gradient {
        GradientStop { position: 0; color: "#eeeeee" }
        GradientStop { position: 1; color: "#777777" }
    }
    width: buttonContent.width + 8
    height: buttonContent.height + 8

    Row {
        id: buttonContent
        anchors.centerIn: parent
        spacing: 4

        Image {
            id: buttonIcon
            source: "qrc:/edit-undo.png"
        }
        Text {
            id: buttonText
            text: "ButtonText"
        }
    }
}</pre>
<p>You'll get the following output:</p>
<div><img src="img/995933a6-b01b-4bb2-bd3d-14d3b60dfafc.png" style="width:20.50em;height:8.58em;" width="309" height="129"/></div>


            

            
        
    </div>



  
<div><h1 class="header-title">What just happened?</h1>
                
            
            
                
<p><kbd>Row</kbd> is a <strong>positioner</strong> QML type provided by the <kbd>QtQuick</kbd> module. Its purpose is similar to the <kbd>RowLayout</kbd> type from the <kbd>QtQuick.Layouts</kbd> module. The <kbd>Row</kbd> item spreads its children in a horizontal row. It makes it possible to position a series of items without using anchors. <kbd>Row</kbd> has the <kbd>spacing</kbd> property that dictates how much space to leave between items.</p>
<p>The <kbd>QtQuick</kbd> module also contains the <kbd>Column</kbd> type that arranges children in a column, the <kbd>Grid</kbd> type that creates a grid of items, and the <kbd>Flow</kbd> type that positions its children side by side, wrapping as necessary.</p>


            

            
        
    </div>



  
<div><h1 class="header-title">Time for action – Sizing the button properly</h1>
                
            
            
                
<p>Our current panel definition still doesn't behave well when it comes to sizing the button. If the button content is very small (for example, the icon doesn't exist or the text is very short), the button will not look good. Typically, push buttons enforce a minimum size—if the content is smaller than a specified size, the button will be expanded to the minimum size allowed. Another problem is that the user might want to override the width or height of the item. In such cases, the content of the button should not overflow past the border of the button. Let's fix these two issues by replacing the <kbd>width</kbd> and <kbd>height</kbd> property bindings with the following code:</p>
<pre>clip: true
implicitWidth: Math.max(buttonContent.implicitWidth + 8, 80)
implicitHeight: buttonContent.implicitHeight + 8 </pre>


            

            
        
    </div>



  
<div><h1 class="header-title">What just happened?</h1>
                
            
            
                
<p>The <kbd>implicitWidth</kbd> and <kbd>implicitHeight</kbd> properties can contain the desired size the item wants to have. It's a direct equivalent of <kbd>sizeHint()</kbd> from Qt Widgets. By using these two properties instead of <kbd>width</kbd> and <kbd>height</kbd> (which are bound to <kbd>implicitWidth</kbd> and<br/>
<kbd>implicitHeight</kbd> by default), we allow the user of our component to override those implicit values. When this happens and the user does not set the width or height big enough to contain the icon and text of the button, we prevent the content from crossing the boundaries of the parent item by setting the <kbd>clip</kbd> property to <kbd>true</kbd>.</p>
<div><br/>
Clipping can reduce performance of your game, so use it only when necessary.</div>


            

            
        
    </div>



  
<div><h1 class="header-title">Time for action – Making the button a reusable component</h1>
                
            
            
                
<p>So far, we have been working on a single button. Adding another button by copying the code, changing the identifiers of all components, and setting different bindings to properties is a very tedious task. Instead, we can make our button item a real component, that is, a new QML type that can be instantiated on demand as many times as required.</p>
<p>First, position the text cursor in the beginning of our <kbd>Rectangle</kbd> item and press <em>Alt</em> + <em>Enter</em> on the keyboard to open the refactoring menu, like in the following screenshot:</p>
<div><img src="img/4c4e5a63-dcf9-48c9-b5f0-85aff19c09e6.png" style="width:32.75em;height:16.58em;" width="727" height="368"/></div>
<p>From the menu, choose Move Component into Separate File. In the popup, type in a name for the new type (for example, <kbd>Button</kbd>) and check <kbd>anchors.centerIn</kbd> in the Property assignments for main.qml list:</p>
<div><img src="img/7257c266-1ae3-45cd-8d12-2fb70bf177ad.png" style="width:30.33em;height:22.33em;" width="503" height="370"/></div>
<p>Accept the dialog by clicking on the OK button.</p>


            

            
        
    </div>



  
<div><h1 class="header-title">What just happened?</h1>
                
            
            
                
<p>You can see that we have a new file called <kbd>Button.qml</kbd> in the project, which contains everything the button item used to have, with the exception of the <kbd>id</kbd> and <kbd>anchors.centerIn</kbd> properties. The main file was simplified to the following:</p>
<pre>Window {
    visible: true
    width: 640
    height: 480
    title: qsTr("Hello World")

    Button {
        id: button<br/>        anchors.centerIn: parent
    }
}</pre>
<p><kbd>Button</kbd> has become a component—a definition of a new type of element that can be used the same way as standard QML element types. Remember that QML component names, as well as names of files representing them, need to begin with a capital letter! If you name a file <kbd>button.qml</kbd> instead of <kbd>Button.qml</kbd>, then you will not be able to use <kbd>Button</kbd> as a component name, and trying to use "button" will result in an error message. This works both ways—every QML file starting with a capital letter can be treated as a component definition.</p>
<p>Since we checked <kbd>anchors.centerIn</kbd> in the dialog, this property was not moved to <kbd>Button.qml</kbd>. The reason for that choice is that our button can be put anywhere, so it can't possibly know how it should be positioned. Instead, positioning of the button should be done at the location where we use the component. Now we can edit <kbd>main.qml</kbd> to put the button into a layout or use other positioning properties without having to change the component's code.</p>


            

            
        
    </div>



  
<div><h1 class="header-title">Importing components</h1>
                
            
            
                
<p>A component definition can be used directly by other QML files residing in the same directory as the component definition. In our example, the <kbd>main.qml</kbd> and <kbd>Button.qml</kbd> files are located in the same directory, so you can use the <kbd>Button</kbd> QML type inside <kbd>main.qml</kbd> without having to import anything.</p>
<p>If you need to access a component definition from a file residing elsewhere, you will have to first import the module containing the component in the file where you want to use it. The definition of a module is very simple—it is just a relative path to the <em>directory</em> containing QML files. This means that if you have a file named <kbd>Baz.qml</kbd> in a directory called <kbd>Base/Foo/Bar</kbd> and you want to use the <kbd>Baz</kbd> component from within the  <kbd>Base/Foo/Ham.qml</kbd> file, you will have to put the following import statement in <kbd>Ham.qml</kbd>:</p>
<pre>import "Bar" </pre>
<p>If you want to use the same component from within the <kbd>Base/Spam.qml</kbd> file, you will have to replace the import statement with this:</p>
<pre>import "Foo/Bar" </pre>
<p>Importing a module makes all its components available for use. You can then declare objects of types imported from a certain module.</p>


            

            
        
    </div>



  
<div><h1 class="header-title">QML and virtual resource paths</h1>
                
            
            
                
<p>Our project uses a Qt resource file to make our QML files embedded into the binary and ensure that they are always available to the application, even if the source directory is not present at the computer. During startup, we refer to the main QML file using the <kbd>qrc:/main.qml</kbd> URL. This means that the runtime only sees the file hierarchy in the resource file, and the actual source directory of the project is not taken into account.</p>
<p>The other QML file has the <kbd>qrc:/Button.qml</kbd> URL, so Qt considers them to be in the same virtual directory and everything still works. However, if you create a QML file but forget to add it to the project's resources, Qt will be unable to load that file. Even if the file is present in the same real directory as <kbd>main.qml</kbd>, Qt will only look for it in the virtual <kbd>qrc:/</kbd> directory.</p>
<p>It's possible to add a file to the resources with a prefix, in which case it can have an URL like <kbd>qrc:/some/prefix/Button.qml</kbd>, and the runtime will consider it to be in another virtual directory. That being said, unless you explicitly create a new prefix, you should be fine. If your QML files are arranged in subdirectories, their hierarchy will be preserved when you add them to the resource file.</p>


            

            
        
    </div>



  
<div><h1 class="header-title">Event handlers</h1>
                
            
            
                
<p>Qt Quick is meant to be used for creating user interfaces that are highly interactive. It offers a number of elements for taking input events from the user. In this section, we will go through them and see how you can use them effectively.</p>


            

            
        
    </div>



  
<div><h1 class="header-title">Time for action – Making the button clickable</h1>
                
            
            
                
<p>So far, our component only looks like a button. The next task is to make it respond to mouse input.</p>
<p>The <kbd>MouseArea</kbd> QML type defines a transparent rectangle that exposes a number of properties and signals related to mouse input. Commonly used signals include <kbd>clicked</kbd>, <kbd>pressed</kbd>, and <kbd>released</kbd>. Let's do a couple of exercises to see how the element can be used.</p>
<p>Open the <kbd>Button.qml</kbd> file and add a <kbd>MouseArea</kbd> child item to the button and use anchors to make it fill the whole area of the button. Call the element <kbd>buttonMouseArea</kbd>. Put the following code in the body of the item:</p>
<pre>Rectangle {
    id: button
    // ...
    Row { ... }
    MouseArea {
        id: buttonMouseArea
        anchors.fill: parent
        onClicked: button.clicked()
    }
} </pre>
<p>In addition to this, set the following declaration in the button object just after its ID is declared:</p>
<pre>Rectangle {
    id: button
    signal clicked()
    // ...
} </pre>
<p>To test the modification, go to the <kbd>main.qml</kbd> file and add a signal handler to the button:</p>
<pre>Button {
    id: button<br/>    anchors.centerIn: parent
    onClicked: console.log("Clicked!")
}</pre>
<p>Then, run the program and click on the button. You'll see your message printed to the Qt Creator's console.</p>


            

            
        
    </div>



  
<div><h1 class="header-title">What just happened?</h1>
                
            
            
                
<p>With the <kbd>signal clicked()</kbd> statement, we declared that the button object can emit a signal called <kbd>clicked</kbd>. With the <kbd>MouseArea</kbd> item, we defined a rectangular area (covering the whole button) that reacts to mouse events. Then, we defined <kbd>onClicked</kbd>, which is a signal handler. For every signal an object has, a script can be bound to a handler named like the signal and prefixed with "on"; hence, for the <kbd>clicked</kbd> signal, the handler is called <kbd>onClicked</kbd>, and, for <kbd>valueChanged</kbd>, it is called <kbd>onValueChanged</kbd>.</p>
<p>In this particular case, we have two handlers defined—one for the button where we write a simple statement to the console, and the other for the <kbd>MouseArea</kbd> element where we call the button's signal function, effectively emitting that signal.</p>
<p><kbd>MouseArea</kbd> has even more features, so now let's try putting them to the right use to make our button more feature-rich.</p>


            

            
        
    </div>



  
<div><h1 class="header-title">Time for action – Visualizing button states</h1>
                
            
            
                
<p>Currently, there is no visual reaction to clicking on the button. In the real world, the button has some depth and when you push it and look at it from above, its contents seems to shift a little toward the right and downward. Let's mimic this behavior by making use of the pressed property <kbd>MouseArea</kbd> has, which denotes whether the mouse button is currently being pressed (note that the pressed property is different from the pressed signal that was mentioned earlier). The content of the button is represented by the <kbd>Row</kbd> element, so add the following statements inside its definition:</p>
<pre>Row {
    id: buttonContent
    // ...
    anchors.verticalCenterOffset: buttonMouseArea.pressed ? 1 : 0
    anchors.horizontalCenterOffset: buttonMouseArea.pressed ? 1 : 0
    // ...
} </pre>
<p>We can also make the text change color when the mouse cursor hovers over the button. For this, we have to do two things. First, let's enable receiving hover events on the <kbd>MouseArea</kbd> by setting its <kbd>hoverEnabled</kbd> property:</p>
<pre>hoverEnabled: true </pre>
<p>When this property is set, <kbd>MouseArea</kbd> will be setting its <kbd>containsMouse</kbd> property to <kbd>true</kbd> whenever it detects the mouse cursor over its own area. We can use this value to set the text color:</p>
<pre>Text {
  id: buttonText
  text: "ButtonText"
  color: buttonMouseArea.containsMouse ? "white" : "black"
} </pre>


            

            
        
    </div>



  
<div><h1 class="header-title">What just happened?</h1>
                
            
            
                
<p>In the last exercise, we learned to use some properties and signals from <kbd>MouseArea</kbd> to make the button component more interactive. However, the element is much richer in features. In particular, if hover events are enabled, you can get the current mouse position in the item's local coordinate system through the <kbd>mouseX</kbd> and <kbd>mouseY</kbd> properties that return values. The cursor position can also be reported by handling the <kbd>positionChanged</kbd> signal. Speaking of signals, most <kbd>MouseArea</kbd> signals carry a <kbd>MouseEvent</kbd> object as their argument. This argument is called <kbd>mouse</kbd> and contains useful information about the current state of the mouse, including its position and buttons currently pressed. By default, <kbd>MouseArea</kbd> only reacts to the left mouse button, but you can use the <kbd>acceptedButtons</kbd> property to select which buttons it should handle. These features are shown in the following example:</p>
<pre>MouseArea {<br/>    id: buttonMouseArea
    anchors.fill: parent
    hoverEnabled: true
    acceptedButtons: Qt.LeftButton | Qt.MiddleButton | Qt.RightButton
    onClicked: {
        switch(mouse.button) {
            case Qt.LeftButton:
                console.log("Left button clicked"); break;
            case Qt.MiddleButton:
                console.log("Middle button clicked"); break;
            case Qt.RightButton:
                console.log("Right button clicked"); break;
        }
    }
    onPositionChanged: {
        console.log("Position: [" + mouse.x + "; " + mouse.y + "]");
    }
} </pre>


            

            
        
    </div>



  
<div><h1 class="header-title">Time for action – Notifying the environment about button states</h1>
                
            
            
                
<p>We have added some code to make the button look more natural by changing its visual aspects. Now, let's extend the button programming interface so that developers can use more features of the button.</p>
<p>The first thing we can do is make button colors definable by introducing some new properties for the button. Let's put the highlighted code at the beginning of the button component definition:</p>
<pre>Rectangle {
    id: button
    property color topColor: "#eeeeee"
    property color bottomColor: "#777777"
    property color textColor: "black"
    property color textPressedColor: "white"
    signal clicked() </pre>
<p>Then, we'll use the new definitions for the background gradient:</p>
<pre>gradient: Gradient {
    GradientStop { position: 0; color: button.topColor }
    GradientStop { position: 1; color: button.bottomColor }
} </pre>
<p>Now for the text color:</p>
<pre>Text {
    id: buttonText
    text: "ButtonText"
    color: buttonMouseArea.pressed ?
        button.textPressedColor : button.textColor
} </pre>
<p>As you can note, we used the <kbd>pressed</kbd> property of <kbd>MouseArea</kbd> to detect whether a mouse button is currently being pressed on the area. We can equip our button with a similar property. Add the following code to the top level <kbd>Rectangle</kbd> of the <kbd>Button</kbd> component:</p>
<pre>property alias pressed: buttonMouseArea.pressed </pre>


            

            
        
    </div>



  
<div><h1 class="header-title">What just happened?</h1>
                
            
            
                
<p>The first set of changes introduced four new properties defining four colors that we later used in statements defining gradient and text colors for the button. In QML, you can define new properties for objects with the <kbd>property</kbd> keyword. The keyword should be followed by the property type and property name. QML understands many property types, the most common being <kbd>int</kbd>, <kbd>real</kbd>, <kbd>string</kbd>, <kbd>font</kbd>, and <kbd>color</kbd>. Property definitions can contain an optional default value for the property, preceded with a colon. The situation is different with the pressed property definition.</p>
<p>You can see that for the property type, the definition contains the word <kbd>alias</kbd>. It is not a property type but an indicator that the property is really an alias to another property—each time the <kbd>pressed</kbd> property of the button is accessed, the value of the <kbd>buttonMouseArea.pressed</kbd> property is returned, and every time the property is changed, it is the mouse area's property that really gets changed. With a regular property declaration, you can provide any valid expression as the default value because the expression is bound to the property. With a property alias, it is different—the value is mandatory and has to be pointing to an existing property of the same or another object.</p>
<p>Consider the following two definitions:</p>
<pre>property int foo: someobject.prop
property alias bar: someobject.prop </pre>
<p>At first glance, they are similar as they point to the same property and therefore the values returned for the properties are the same. However, the properties are really very different, which becomes apparent if you try to modify their values:</p>
<pre>  foo = 7
  bar = 7 </pre>
<p>The first property actually has an expression bound to it, so assigning <kbd>7</kbd> to <kbd>foo</kbd> simply releases the binding and assigns the value <kbd>7</kbd> to the <kbd>foo</kbd> property, leaving <kbd>someobject.prop</kbd> with its original value. The second statement, however, is an alias; therefore, assigning a new value applies the modification to the <kbd>someobject.prop</kbd> property the alias is really pointing to.</p>
<p>Speaking of properties, there is an easy way to react when a property value is modified. For each existing property, there is a handler available that is executed whenever the property value is modified. The handler name is <kbd>on</kbd> followed by the property name, then followed by the word <kbd>Changed</kbd>, all in camel case—thus, for a <kbd>foo</kbd> property, it becomes <kbd>onFooChanged</kbd> and for <kbd>topColor</kbd>, it becomes <kbd>onTopColorChanged</kbd>. To log the current press state of the button to the console, all we need to do is implement the property change handler for this property:</p>
<pre>Button {
    // ...

    onPressedChanged: {
        console.log("The button is currently " +
                    (pressed ? "" : "not ") + "pressed")
    }
}</pre>
<p>In this example, we created a fully functional custom QML component. Our button reacts to mouse input and exposes some useful properties and signals to the user. This makes it a reusable and customizable object. In a real project, always think of the repeating parts of your UI and consider moving them into a single component to reduce code duplication.</p>


            

            
        
    </div>



  
<div><h1 class="header-title">Touch input</h1>
                
            
            
                
<p><kbd>MouseArea</kbd> is the simplest of input event elements. Nowadays, more and more devices have touch capabilities and Qt Quick can handle them as well. Currently, we have three ways of handling touch input.</p>
<p>First of all, simple touch events are also reported as mouse events. Tapping and sliding a finger on the screen can be handled using <kbd>MouseArea</kbd>, just like mouse input.</p>


            

            
        
    </div>



  
<div><h1 class="header-title">Time for action – Dragging an item around</h1>
                
            
            
                
<p>Create a new Qt Quick Application - Empty project. Edit the <kbd>main.qml</kbd> file to add a circle to the window:</p>
<pre>Rectangle {
    id: circle
    width: 60; height: width
    radius: width / 2
    color: "red"
} </pre>
<p>Next, add a <kbd>MouseArea</kbd> to the circle and use its <kbd>drag</kbd> property to enable moving the circle by touch (or mouse):</p>
<pre>Rectangle {
    //...
    MouseArea {
        anchors.fill: parent
        drag.target: circle
    }
}</pre>
<p>Then, you can start the application and begin moving the circle around.</p>


            

            
        
    </div>



  
<div><h1 class="header-title">What just happened?</h1>
                
            
            
                
<p>A circle was created by defining a rectangle with its height equal to width, making it a square and rounding the borders to half the side length. The <kbd>drag</kbd> property can be used to tell <kbd>MouseArea</kbd> to manage a given item's position using input events flowing into this <kbd>MouseArea</kbd> element. We denote the item to be dragged using the <kbd>target</kbd> subproperty. You can use other subproperties to control the axis the item is allowed to move along or constrain the move to a given area. An important thing to remember is that the item being dragged cannot be anchored for the axis on which the drag is requested; otherwise, the item will respect the anchor and not the drag. We didn't anchor our circle item at all since we want it to be draggable along both axes.</p>
<p>The second approach to handling touch input in Qt Quick applications is to use <kbd>PinchArea</kbd>, which is an item similar to <kbd>MouseArea</kbd>, but rather than dragging an item around, it allows you to rotate or scale it using two fingers (with a so called "pinch" gesture), as shown:</p>
<div><img src="img/aceb744d-bcdc-470b-b636-76654417b369.png" style="width:27.33em;height:19.83em;" width="1492" height="1082"/></div>
<p>Be aware that <kbd>PinchArea</kbd> reacts only to touch input, so to test the example, you will need a real multitouch capable device.</p>


            

            
        
    </div>



  
<div><h1 class="header-title">Time for action – Rotating and scaling a picture by pinching</h1>
                
            
            
                
<p>Start a new Qt Quick Application - Empty project. Add an image file to the resources, just like we previously did in the button project. In the <kbd>main.qml</kbd> file, add an image to the window and make it centered in its parent:</p>
<pre>Image {
    id: image
    anchors.centerIn: parent
    source: "qrc:/wilanow.jpg"
}</pre>
<p>Now, we will add a <kbd>PinchArea</kbd> element. This kind of item can be used in two ways—either by manually implementing signal handlers <kbd>onPinchStarted</kbd>, <kbd>onPinchUpdated</kbd>, and <kbd>onPinchFinished</kbd> to have total control over the functionality of the gesture, or using a simplified interface similar to the <kbd>drag</kbd> property of <kbd>MouseArea</kbd>. Since the simplified interface does exactly what we want, there is no need to handle pinch events manually. Let's add the following declaration to the file:</p>
<pre>PinchArea {
    anchors.fill: parent
    pinch {
        target: image
        minimumScale: 0.2
        maximumScale: 2.0
        minimumRotation: -90
        maximumRotation: 90
    }
}</pre>
<p>You'll get an output similar to the following screenshot:</p>
<div><img src="img/12e68796-9807-4696-93d9-c65d6fdf5c94.png" style="width:26.92em;height:28.33em;" width="371" height="391"/></div>


            

            
        
    </div>



  
<div><h1 class="header-title">What just happened?</h1>
                
            
            
                
<p>Our simple application loads an image and centers it in the view. Then, there is a <kbd>PinchArea</kbd> item filling the view area that is told to operate on the image object. We define the range of the scaling and rotating of the item. The rest is left to the <kbd>PinchArea</kbd> item itself. If you start interacting with the application, you will see the item rotate and scale. What really happens behind the scenes is that <kbd>PinchArea</kbd> modifies the values of the two properties each Qt Quick item has—<kbd>rotation</kbd> and <kbd>scale</kbd>.</p>
<div><kbd>PinchArea</kbd> can also control the dragging of the item with <kbd>pinch.dragAxis</kbd>, just like <kbd>MouseArea</kbd> does with drag, but for simplicity, we didn't use this part of the API. Feel free to experiment with it in your own code.</div>


            

            
        
    </div>



  
<div><h1 class="header-title">Have a go hero – Rotating and scaling with a mouse</h1>
                
            
            
                
<p>Of course, you don't have to use <kbd>PinchArea</kbd> to rotate or scale an item. Properties controlling those aspects are regular properties that you can read and write at any time. Try replacing <kbd>PinchArea</kbd> with <kbd>MouseArea</kbd> to obtain a result similar to what we just did by modifying the scale and rotation properties as a result of receiving mouse events—when the user drags the mouse while pressing the left button, the image is scaled and when the user does the same while pressing the right button, the image is rotated.</p>
<p>If you manage to do the task, try replacing <kbd>MouseArea</kbd> with <kbd>PinchArea</kbd> again, but then, instead of using the <kbd>pinch</kbd> property, handle events manually to obtain the same effect (the event object is called <kbd>pinch</kbd> and has a number of properties you can play with).</p>
<p>A third approach to handling touch input is using the <kbd>MultiPointTouchArea</kbd> item. It provides a low-level interface to gestures by reporting each touch point separately. It can be used to create custom high-level gesture handlers similar to <kbd>PinchArea</kbd>.</p>


            

            
        
    </div>



  
<div><h1 class="header-title">Keyboard input</h1>
                
            
            
                
<p>So far, we've been dealing with pointer input, but user input is not just that—we can also handle keyboard input. This is quite simple and basically boils down to two easy steps.</p>
<p>First, you have to enable receiving keyboard events by stating that a particular item has keyboard focus:</p>
<pre>focus: true </pre>
<p>Then, you can start handling events by writing handlers in a similar fashion as for mouse events. However, <kbd>Item</kbd> doesn't provide its own handler for manipulating keys that is a counterpart for <kbd>keyPressEvent</kbd> and <kbd>keyReleaseEvent</kbd> of <kbd>QWidget</kbd>. Instead, adequate handlers are provided by the <kbd>Keys</kbd> attached property.</p>
<p>Attached properties are provided by elements that are not used as standalone elements but provide properties to other objects by getting attached to them as well. This is a way of adding support for new properties without modifying the API of the original element (it doesn't add new properties through an <strong>is-a</strong> relation, but rather through a <strong>has-a</strong> one). Each object that references an attached property gets its own copy of the attaching object that then handles the extra properties. We will come back to attached properties later in the chapter. For now, you just need to remember that in certain situations, an element can obtain additional properties that are not part of its API.</p>
<p>Let's go back to implementing event handlers for keyboard input. As we said earlier, each Item has a <kbd>Keys</kbd> attached property that allows us to install our own keyboard handlers. The basic two signals <kbd>Keys</kbd> adds to <kbd>Item</kbd> are <kbd>pressed</kbd> and <kbd>released</kbd>; therefore, we can implement the <kbd>onPressed</kbd> and <kbd>onReleased</kbd> handlers that have a <kbd>KeyEvent</kbd> argument providing similar information as <kbd>QKeyEvent</kbd> in the widget world. As an example, we can see an item that detects when the spacebar was pressed:</p>
<pre>Rectangle {
    focus: true
    color: "black"
    width: 100
    height: 100
    Keys.onPressed: {
        if(event.key === Qt.Key_Space) {
             color = "red";
        }
    }
    Keys.onReleased: {
        if(event.key === Qt.Key_Space) {
            color = "blue";
        }
    }
}</pre>
<p>It might become problematic if you want to handle many different keys in the same item, as the <kbd>onPressed</kbd> handler would likely contain a giant switch section with branches for every possible key. Fortunately, <kbd>Keys</kbd> offers more properties. Most of the commonly used keys (but not letters) have their own handlers that are called when the particular key is pressed. Thus, we can easily implement an item that takes a different color depending on which key was pressed last:</p>
<pre>Rectangle {<br/>    //...
    focus: true
    Keys.onSpacePressed:      color = "purple"
    Keys.onReturnPressed:     color = "navy"
    Keys.onVolumeUpPressed:   color = "blue"
    Keys.onRightPressed:      color = "green"
    Keys.onEscapePressed:     color = "yellow"
    Keys.onTabPressed:        color = "orange"
    Keys.onDigit0Pressed:     color = "red"
} </pre>
<p>Note that the <kbd>released</kbd> signal will still be emitted for every released key even if the key has its own pressed signal.</p>
<p>Now, consider another example:</p>
<pre>Item {
    id: item
    property int number: 0
    width: 200; height: width
    focus: true
<strong>    Keys.onSpacePressed: {</strong><br/><strong>        number++;</strong><br/><strong>    }</strong>
    Text {
        text: item.number
        anchors.centerIn: parent
    }
}</pre>
<p>We would expect that when we press and hold the spacebar, we will see the text change from <kbd>0</kbd> to <kbd>1</kbd> and stay on that value until we release the key. If you run the example, you will see that instead, the number keeps incrementing as long as you hold down the key. This is because by default, the keys autorepeat—when you hold the key, the operating system keeps sending a sequence of press-release events for the key (you can verify that by adding the <kbd>console.log()</kbd> statements to the <kbd>Keys.onPressed</kbd> and <kbd>Keys.onReleased</kbd> handlers). To counter this effect, you can differentiate between autorepeat and regular events. In Qt Quick, you can do this easily, as each key event carries the appropriate information. Simply replace the handler from the last example with the following one:</p>
<pre>Keys.onSpacePressed: {<br/>    if(!event.isAutoRepeat) {<br/>        number++;<br/>    }<br/>}</pre>
<p>The event variable we use here is the name of the parameter of the <kbd>spacePressed</kbd> signal. As we cannot declare our own names for the parameters like we can do in C++, for each signal handler, you will have to look up the name of the argument in the documentation. You can search for <kbd>Keys</kbd> in the documentation index to open the Keys QML Type page. The signal list will contain type and name of the signal's parameter, for example, <kbd>spacePressed(KeyEvent event)</kbd>.</p>
<p>Whenever you process an event, you should mark it as accepted to prevent it from being propagated to other elements and handled by them:</p>
<pre>Keys.onPressed: {<br/>    if(event.key === Qt.Key_Space) {<br/>        color = "blue";<br/>        <strong>event.accepted = true;</strong><br/>    }<br/>}</pre>
<p>However, if you use a handler dedicated to an individual button (like <kbd>onSpacePressed</kbd>), you don't need to accept the event, as Qt will do that for you automatically.</p>
<p>In standard C++ applications, we usually use the <em>Tab</em> key to navigate through focusable items. With games (and fluid user interfaces in general), it is more common to use arrow keys for item navigation. Of course, we can handle this situation using the <kbd>Keys</kbd> attached property and adding <kbd>Keys.onRightPressed</kbd>, <kbd>Keys.onTabPressed</kbd>, and other signal handlers to each of our items where we want to modify the focus property of the desired item, but it would quickly clutter our code. Qt Quick comes to our help once again by providing a <kbd>KeyNavigation</kbd> attached property, which is meant to handle this specific situation and allows us to greatly simplify the needed code. Now, we can just specify which item should get into focus when a specific key is triggered:</p>
<pre>Row {
    spacing: 5
    Rectangle {
        id: first
        width: 50; height: width
        color: focus ? "blue" : "lightgray"
        focus: true
        <strong>KeyNavigation.right: second</strong>
    }
    Rectangle {
        id: second
        width: 50; height: width
        color: focus ? "blue" : "lightgray"
        <strong>KeyNavigation.right: third</strong>
    }
    Rectangle {
        id: third
        width: 50; height: width
        color: focus ? "blue" : "lightgray"
    }
} </pre>
<p>Note that we made the first item get into focus in the beginning by explicitly setting the <kbd>focus</kbd> property. By setting the <kbd>KeyNavigation.right</kbd> property, we instruct Qt to focus on the specified item when this item receives a right key press event. The reverse transition is added automatically—when the left key is pressed on the second item, the first item will receive focus. Besides <kbd>right</kbd>, <kbd>KeyNavigation</kbd> contains the <kbd>left</kbd>, <kbd>down</kbd>, <kbd>up</kbd>, <kbd>tab</kbd>, and <kbd>backtab</kbd> (<em>Shift</em> + <em>Tab</em>) properties.</p>
<p>Both the <kbd>Keys</kbd> and <kbd>KeyNavigation</kbd> attached properties have a way to define<br/>
the order in which each of the mechanisms receive the events. This is handled by the <kbd>priority</kbd> property, which can be set to either <kbd>BeforeItem</kbd> or <kbd>AfterItem</kbd>. By default, <kbd>Keys</kbd> will get the event first (<kbd>BeforeItem</kbd>), then the internal event handling can take place and finally, <kbd>KeyNavigation</kbd> will have a chance of handling the event (<kbd>AfterItem</kbd>). Note that if the key is handled by one of the mechanisms, the event is accepted and the remaining mechanisms will not receive that event.</p>


            

            
        
    </div>



  
<div><h1 class="header-title">Have a go hero – Practicing key-event propagation</h1>
                
            
            
                
<p>As an exercise, you can expand our last example by building a larger array of items (you can use the <kbd>Grid</kbd> element to position them) and defining a navigation system that makes use of the <kbd>KeyNavigation</kbd> attached property. Have some of the items handle events themselves using the <kbd>Keys</kbd> attached property. See what happens when the same key is handled by both mechanisms. Try influencing the behavior using the <kbd>priority</kbd> property.</p>
<p>When you set the <kbd>focus</kbd> property of an item to <kbd>true</kbd>, any previously used item loses focus. This becomes a problem when you try to write a reusable component that needs to set focus to its children. If you add multiple instances of such a component to a single window, their focus requests will conflict with each other. Only the last item will have focus because it was created last. To overcome this problem, Qt Quick introduces a concept of <strong>focus scopes</strong>. By wrapping your component into a <kbd>FocusScope</kbd> item, you gain ability to set focus to an item inside the component without influencing the global focus directly. When an instance of your component receives focus, the internal focused item will also receive focus and will be able to handle keyboard events. A good explanation of this feature is given on the Keyboard Focus in Qt Quick documentation page.</p>


            

            
        
    </div>



  
<div><h1 class="header-title">Text input fields</h1>
                
            
            
                
<p>Apart from the attached properties we described, Qt Quick provides built-in elements for handling keyboard input. The two most basic types are <kbd>TextInput</kbd> and <kbd>TextEdit</kbd>, which are QML equivalents of <kbd>QLineEdit</kbd> and <kbd>QTextEdit</kbd>. The former are used for single-line text input, while the latter serves as its multiline counterpart. They both offer cursor handling, undo-redo functionality, and text selections. You can validate text typed into <kbd>TextInput</kbd> by assigning a validator to the <kbd>validator</kbd> property. For example, to obtain an item where the user can input a dot-separated IP address, we can use the following declaration:</p>
<pre>TextInput {
    id: ipAddress
    width: 100
    validator: RegExpValidator {
        // four numbers separated by dots
        regExp: /\d+\.\d+\.\d+\.\d+/
    }
    focus: true
} </pre>
<p>The regular expression only verifies the format of the address. The user can still insert bogus numbers. You should either do a proper check before using the address or provide a more complex regular expression that will constrain the range of numbers the user can enter.</p>
<p>One thing to remember is that neither <kbd>TextInput</kbd> nor <kbd>TextEdit</kbd> has any visual appearance (apart from the text and cursor they contain), so if you want to give the user some visual hint as to where the item is positioned, the easiest solution is to wrap it in a styled rectangle:</p>
<pre>Rectangle {
  id: textInputFrame
  width: 200
  height: 40
  border { color: "black"; width: 2 }
  radius: 10
  antialiasing: true
  color: "darkGray"
}
TextInput {
  id: textInput
  anchors.fill: textInputFrame
  anchors.margins: 5
  font.pixelSize: height-2
  verticalAlignment: TextInput.AlignVCenter
  clip: true
} </pre>
<p>Note that the highlighted code—the <kbd>clip</kbd> property of <kbd>textInput</kbd>—is enabled such that by default, if the text entered in the box doesn't fit in the item, it will overflow it and remain visible outside the actual item. By enabling clipping, we explicitly say that anything that doesn't fit the item should not be drawn.</p>
<div><img src="img/b2e5449b-d3b5-4db5-a7b5-089516765a84.png" style="width:10.58em;height:2.58em;" width="222" height="53"/></div>
<p>The <kbd>QtQuick.Controls</kbd> module provides more advanced text input controls, such as <kbd>TextField</kbd> and <kbd>TextArea</kbd>. We've already used them in our project in <a href="b81d9c47-58fa-49dd-931a-864c7be05840.xhtml">Chapter 11</a>, <em>Introduction to Qt Quick</em>.</p>


            

            
        
    </div>



  
<div><h1 class="header-title">Gamepad input</h1>
                
            
            
                
<p>Handling gamepad events is a very common task when developing a game. Fortunately, Qt provides Qt Gamepad module for this purpose. We already learned how to use it in C++. Now let's see how to do this in QML applications.</p>
<p>To enable Qt Gamepad module, add <kbd>QT += gamepad</kbd> to the project file. Next, import the QML module by adding the following line at the beginning of your QML file:</p>
<pre>import QtGamepad 1.0</pre>
<p>This import allows you to declare objects of the <kbd>Gamepad</kbd> type. Add the following object inside your top-level QML object:</p>
<pre> Gamepad {<br/>     id: gamepad<br/>     deviceId: GamepadManager.connectedGamepads.length &gt; 0 ? <br/>         GamepadManager.connectedGamepads[0] : -1<br/> }</pre>
<p>The <kbd>GamepadManager</kbd> object allows us to list identifiers of gamepads available in the system. We use the first available gamepad if any are present in the system. If you want the game to pick up a connected gamepad on the fly, use the following code snippet:</p>
<pre>    Connections {<br/>        target: GamepadManager<br/>        onGamepadConnected: gamepad.deviceId = deviceId<br/>    }</pre>


            

            
        
    </div>



  
<div><h1 class="header-title">What just happened?</h1>
                
            
            
                
<p>The preceding code simply adds a signal handler for the <kbd>gamepadConnected</kbd> signal of the <kbd>GamepadManager</kbd> object. The usual way to add a signal handler is to declare it directly in the section of the sender. However, we can't do that in this case, since  <kbd>GamepadManager</kbd> is an existing global object that is not part of our QML object tree. Thus, we use the <kbd>Connections</kbd> QML type that allows us to specify an arbitrary sender (using the <kbd>target</kbd> property) and attach a signal handler to it. You can think of <kbd>Connections</kbd> as a declarative version of <kbd>QObject::connect</kbd> calls.</p>
<p>The initialization is done, so we can now use the <kbd>gamepad</kbd> object to request information about the gamepad input. There are two ways to do that.</p>
<p>First, we can use property bindings to set properties of other objects depending on the buttons pressed on the gamepad:</p>
<pre>Text {<br/>    text: gamepad.buttonStart ? "Start!" : ""<br/>}</pre>
<p>Whenever the start button is pressed or released on the gamepad, the value of the <kbd>gamepad.buttonStart</kbd> property will be set to <kbd>true</kbd> or <kbd>false</kbd>, and the QML engine will automatically update the displayed text.</p>
<p>The second way is to add a signal handler to detect when a property changes:</p>
<pre>Gamepad {<br/>    //...<br/>    onButtonStartChanged: {<br/>        if (value) {<br/>            console.log("start pressed");<br/>        } else {<br/>            console.log("start released");<br/>        }<br/>    }<br/>}</pre>
<p>The <kbd>Gamepad</kbd> QML type has a separate property and signal for each gamepad button, just like the <kbd>QGamepad</kbd> C++ class.</p>
<p>You can also use the <kbd>GamepadKeyNavigation</kbd> QML type to introduce gamepad support to a game that supports keyboard input:</p>
<pre>GamepadKeyNavigation {<br/>    gamepad: gamepad<br/>    active: true<br/>    buttonStartKey: Qt.Key_S<br/>}</pre>
<p>When this object is declared in your QML file, gamepad events provided by the <kbd>gamepad</kbd> object will be automatically converted to key events. By default, <kbd>GamepadKeyNavigation</kbd> is able to emulate up, down, left, right, back, forward, and return keys when the corresponding gamepad buttons are pressed. However, you can override the default mapping or add your own mapping for other gamepad buttons. In the preceding example, we tell <kbd>GamepadKeyNavigation</kbd> that the start key on the gamepad should act as if the <em>S</em> key was pressed on the keyboard. You can now handle these events just as any regular keyboard event.</p>


            

            
        
    </div>



  
<div><h1 class="header-title">Sensor input</h1>
                
            
            
                
<p>Qt is reaching out to more and more platforms that are used nowadays. This includes a number of popular mobile platforms. Mobile devices are usually equipped with additional hardware, less often seen on desktop systems. Let's see how to handle sensor input in Qt so that you can use it in your games.</p>
<p>Most of the features discussed in this section are not usually available on desktops. If you want to play with them, you need to set up running Qt applications on a mobile device. This requires a few configuration steps that depend on your target platform. Please refer to Qt documentation for exact instructions, as they will offer complete and up-to-date information that wouldn't be possible to provide in a book. Good starting points are Getting Started with Qt for Android  and Qt for iOS documentation pages.</p>
<p>Access to sensors present on mobile devices is provided by the Qt Sensors module and must be imported before it can be used:</p>
<pre>import QtSensors 5.0</pre>
<p>There are a lot of QML types you can use to interact with sensors. Have a look at this impressive list:</p>
<table class="table">
<thead>
<tr>
<td><strong>QML type</strong></td>
<td><strong>Description</strong></td>
</tr>
</thead>
<tbody>
<tr>
<td>
<p><kbd>Accelerometer</kbd></p>
</td>
<td>
<p>Reports the device's linear acceleration along the <em>x</em>, <em>y</em>, and <em>z</em> axes.</p>
</td>
</tr>
<tr>
<td>
<p><kbd>Altimeter</kbd></p>
</td>
<td>
<p>Reports the altitude in meters relative to mean sea level.</p>
</td>
</tr>
<tr>
<td>
<p><kbd>AmbientLightSensor</kbd></p>
</td>
<td>
<p>Reports the intensity of the ambient light.</p>
</td>
</tr>
<tr>
<td>
<p><kbd>AmbientTemperatureSensor</kbd></p>
</td>
<td>
<p>Reports the temperature in degree Celsius of the current device's ambient.</p>
</td>
</tr>
<tr>
<td>
<p><kbd>Compass</kbd></p>
</td>
<td>
<p>Reports the azimuth of the device's top as degrees from magnetic north.</p>
</td>
</tr>
<tr>
<td>
<p><kbd>DistanceSensor</kbd></p>
</td>
<td>
<p>Reports distance in cm from an object to the device.</p>
</td>
</tr>
<tr>
<td>
<p><kbd>Gyroscope</kbd></p>
</td>
<td>
<p>Reports the device's movement around its axes in degrees per second.</p>
</td>
</tr>
<tr>
<td>
<p><kbd>HolsterSensor</kbd></p>
</td>
<td>
<p>Reports if the device is holstered in a specific pocket.</p>
</td>
</tr>
<tr>
<td>
<p><kbd>HumiditySensor</kbd></p>
</td>
<td>
<p>Reports on humidity.</p>
</td>
</tr>
<tr>
<td>
<p><kbd>IRProximitySensor</kbd></p>
</td>
<td>
<p>Reports the reflectance of the outgoing infrared light. The range is from 0 (zero reflection) to 1 (total reflection).</p>
</td>
</tr>
<tr>
<td>
<p><kbd>LidSensor</kbd></p>
</td>
<td>
<p>Reports on whether a device is closed.</p>
</td>
</tr>
<tr>
<td>
<p><kbd>LightSensor</kbd></p>
</td>
<td>
<p>Reports the intensity of light in lux.</p>
</td>
</tr>
<tr>
<td>
<p><kbd>Magnetometer</kbd></p>
</td>
<td>
<p>Reports the device's magnetic flux density along its axes.</p>
</td>
</tr>
<tr>
<td>
<p><kbd>OrientationSensor</kbd></p>
</td>
<td>
<p>Reports the orientation of the device.</p>
</td>
</tr>
<tr>
<td>
<p><kbd>PressureSensor</kbd></p>
</td>
<td>
<p>Reports the atmospheric pressure in Pascals.</p>
</td>
</tr>
<tr>
<td>
<p><kbd>ProximitySensor</kbd></p>
</td>
<td>
<p>Reports if an object is close to the device. Which distance is considered "close" depends on the device.</p>
</td>
</tr>
<tr>
<td>
<p><kbd>RotationSensor</kbd></p>
</td>
<td>
<p>Reports the three angles that define the orientation of the device in a three-dimensional space.</p>
</td>
</tr>
<tr>
<td>
<p><kbd>TapSensor</kbd></p>
</td>
<td>
<p>Reports if a device was tapped.</p>
</td>
</tr>
<tr>
<td>
<p><kbd>TiltSensor</kbd></p>
</td>
<td>
<p>Reports the angle of tilt in degrees along the device's axes.</p>
</td>
</tr>
</tbody>
</table>
<p>Unfortunately, not all sensors are supported on all platforms. Check out the Compatibility Map documentation page to see which sensors are supported on your target platforms before trying to use them.</p>
<p>All these types inherit the <kbd>Sensor</kbd> type and provide similar API. First, create a sensor object and activate it by setting its <kbd>active</kbd> property to <kbd>true</kbd>. When the hardware reports new values, they are assigned to the sensor's <kbd>reading</kbd> property. As with any property in QML, you can choose between using the property directly, using it in a property binding, or using the <kbd>onReadingChanged</kbd> handler to react to each new value of the property.</p>
<p>The type of the <kbd>reading</kbd> object corresponds to the type of the sensor. For example, if you use a tilt sensor, you'll receive a <kbd>TiltReading</kbd> object that provides suitable properties to access the angle of tilt around the <em>x</em> (<kbd>xRotation</kbd>) and <em>y</em> (<kbd>yRotation</kbd>) axes. For each sensor type, Qt provides a corresponding reading type that contains the sensor data.</p>
<p>All readings also have the <kbd>timestamp</kbd> property that contains the number of microseconds since some fixed point. That point can be different for different sensor objects, so you can only use it to calculate time intervals between two readings of the same sensor.</p>
<p>The following QML code contains a complete example of using a tilt sensor:</p>
<pre>import QtQuick 2.9<br/>import QtQuick.Window 2.2<br/>import QtSensors 5.0<br/>Window {<br/>    visible: true<br/>    width: 640<br/>    height: 480<br/>    title: qsTr("Hello World")<br/>    Text {<br/>        anchors.centerIn: parent<br/>        text: {<br/>            if (!tiltSensor.reading) {<br/>                return "No data";<br/>            }<br/>            var x = tiltSensor.reading.xRotation;<br/>            var y = tiltSensor.reading.yRotation;<br/>            return "X: " + Math.round(x) +<br/>                   " Y: " + Math.round(y)<br/>        }<br/>    }<br/>    TiltSensor {<br/>        id: tiltSensor<br/>        active: true<br/>        onReadingChanged: {<br/>            // process new reading<br/>        }<br/>    }<br/>}</pre>
<p>When this application receives a new reading, the text on screen will be automatically updated. You can also use the <kbd>onReadingChanged</kbd> handler to process new data in another way.</p>


            

            
        
    </div>



  
<div><h1 class="header-title">Detecting device location</h1>
                
            
            
                
<p>Some modern games require information about the player's geographic location and other related data, such as movement speed. The Qt Positioning module allows you to access this information. Let's see a basic QML example of determining the location:</p>
<pre>import QtQuick 2.9<br/>import QtQuick.Window 2.2<br/>import QtPositioning 5.0<br/>Window {<br/>    visible: true<br/>    width: 640<br/>    height: 480<br/>    title: qsTr("Hello World")<br/>    Text {<br/>        anchors.centerIn: parent<br/>        text: {<br/>            var pos = positionSource.position;<br/>            var coordinate = pos.coordinate;<br/>            return "latitude: " + coordinate.latitude +<br/>              "\nlongitude: " + coordinate.longitude;<br/>        }<br/>    }<br/>    PositionSource {<br/>        id: positionSource<br/>        active: true<br/>        onPositionChanged: {<br/>            console.log("pos changed",<br/>                        position.coordinate.latitude,<br/>                        position.coordinate.longitude);<br/>        }<br/>    }<br/>}</pre>
<p>First, we import the <kbd>QtPositioning</kbd> module into scope. Next, we create a <kbd>PositionSource</kbd> object and set its <kbd>active</kbd> property to true. The <kbd>position</kbd> property of the <kbd>PositionSource</kbd> object will be automatically updated as new information is available. In addition to latitude and longitude, this property also contains information about altitude, direction and speed of travel, and accuracy of location. Since some of values may not be available, each value is accompanied with a Boolean property that indicates if the data is present. For example, if <kbd>directionValid</kbd> is true, then <kbd>direction</kbd> value was set.</p>
<p>There are multiple ways to determine the player's location. The <kbd>PositionSource</kbd> type has a few properties that allow you to specify the source of the data. First, the <kbd>preferredPositioningMethods</kbd>  property allows you to choose between satellite data, non-satellite data, or using both of them. The <kbd>supportedPositioningMethods</kbd> property holds information about currently available methods. You can also use the <kbd>nmeaSource</kbd> property to provide an NMEA position-specification data file which overrides any other data sources and can be used to simulate the device's location and movement which is very useful during development and testing of the game. </p>


            

            
        
    </div>



  
<div><h1 class="header-title">Creating advanced QML components</h1>
                
            
            
                
<p>By now, you should be familiar with the very basics of QML and Qt Quick. Now, we can start combining what you know and fill the gaps with more information to build more advanced QML components. Our target will be to display an analog clock.</p>


            

            
        
    </div>



  
<div><h1 class="header-title">Time for action – A simple analog clock application</h1>
                
            
            
                
<p>Create a new Qt Quick Application - Empty project. To create a clock, we will implement a component representing the clock needle, and we will use instances of that component in the actual clock element. In addition to this, we will make the clock a reusable component; therefore, we will create it in a separate file and instantiate it from within <kbd>main.qml</kbd>:</p>
<pre>Window {
    visible: true
    width: 640
    height: 480
    title: qsTr("Hello World")

    Clock {
        id: clock
        anchors {
            fill: parent
            margins: 20
        }
    }
}</pre>
<p>We use the <kbd>anchors</kbd> property group to expand the item to fit the whole window except for the 20-pixel margin for all four sides.</p>
<p>Before this code works, however, we need to add a new QML file for the <kbd>Clock</kbd> component. Locate the <kbd>qml.qrc</kbd> resource file in the project tree and select Add New... in its context menu. From the Qt category, select QML File (Qt Quick 2), input <kbd>Clock</kbd> as the name and confirm the operation. A new file called <kbd>Clock.qml</kbd> will be created and added to the resources list.</p>
<p>Let's start by declaring a circular clock plate:</p>
<pre>import QtQuick 2.9

Item {
    id: clock

    property color color: "lightgray"

    Rectangle {
        id: plate

        anchors.centerIn: parent
        width: Math.min(clock.width, clock.height)
        height: width
        radius: width / 2
        color: clock.color
        border.color: Qt.darker(color)
        border.width: 2
    }
} </pre>
<p>If you run the program now, you'll see a plain gray circle hardly resembling a clock plate:</p>
<div><img src="img/9f396b5d-9020-41ce-a194-7a811e1c2543.png" style="width:13.42em;height:13.50em;" width="400" height="400"/></div>
<p>The next step is to add marks dividing the plate into 12 sections. We can do this by putting the following declaration inside the <kbd>plate</kbd> object:</p>
<pre>Repeater {
    model: 12

    Item {
        id: hourContainer

        property int hour: index
        height: plate.height / 2
        transformOrigin: Item.Bottom
        rotation: index * 30
        x: plate.width/2
        y: 0

        Rectangle {
            width: 2
            height: (hour % 3 == 0) ? plate.height * 0.1
                                    : plate.height * 0.05
            color: plate.border.color
            antialiasing: true
            anchors.horizontalCenter: parent.horizontalCenter
            anchors.top: parent.top
            anchors.topMargin: 4
        }
    }
} </pre>
<p>Running the program should now give the following result, looking much more like a clock plate:</p>
<div><img src="img/f9920416-1d71-4d85-a249-cca203a92d26.png" style="width:15.58em;height:15.58em;" width="400" height="400"/></div>


            

            
        
    </div>



  
<div><h1 class="header-title">What just happened?</h1>
                
            
            
                
<p>The code we just created introduces a couple of new features. Let's go through them one by one.</p>
<p>First of all, we used a new element called <kbd>Repeater</kbd>. It does exactly what its name says—it repeats items declared within it using a given model. For each entry in the model, it creates an instance of a component assigned to a property called <kbd>delegate</kbd> (the property name means that it contains an entity to which the caller delegates some responsibility, such as describing a component to be used as a stencil by the caller). <kbd>Item</kbd> declared in <kbd>Repeater</kbd> describes the delegate even though we cannot see it explicitly assigned to a property. This is because <kbd>delegate</kbd> is a default property of the <kbd>Repeater</kbd> type, which means anything unassigned to any property explicitly gets implicitly assigned to the default property of the type.</p>
<p>The <kbd>Item</kbd> type also has a default property called <kbd>data</kbd>. It holds a list of elements that gets automatically split into two "sublists"—the list of the item's children (which creates the hierarchy of <kbd>Item</kbd> instances in Qt Quick) and another list called resources, which contains all "child" elements that do not inherit from <kbd>Item</kbd>. You have direct access to all three lists, which means calling <kbd>children[2]</kbd> will return the third <kbd>Item</kbd> element declared in the item, and <kbd>data[5]</kbd> will return the sixth element declared in the <kbd>Item</kbd>, regardless of whether the given element is a visual item (that inherits <kbd>Item</kbd>) or not.</p>
<p>The model can be a number of things, but in our case, it is simply a number denoting how many times the delegate should be repeated. The component to be repeated is a transparent item containing a rectangle. The item has a property declared called <kbd>hour</kbd> that has the <kbd>index</kbd> variable bound to it. The latter is a property assigned by <kbd>Repeater</kbd> to each instance of the delegate component. The value it contains is the index of the instance in the <kbd>Repeater</kbd> object—since we have a model containing twelve elements, <kbd>index</kbd> will hold values within a range of <kbd>0</kbd> to <kbd>11</kbd>. The item can make use of the <kbd>index</kbd> property to customize instances created by <kbd>Repeater</kbd>. In this particular case, we use <kbd>index</kbd> to provide values for a <kbd>rotation</kbd> property and by multiplying the index by <kbd>30</kbd>, we get values starting from <kbd>0</kbd> for the first instance and ending at <kbd>330</kbd> for the last one.</p>
<p>The <kbd>rotation</kbd> property brings us to the second most important subject—item transformations. Each item can be transformed in a number of ways, including rotating the item and scaling it in two-dimensional space, as we already mentioned earlier. Another property called <kbd>transformOrigin</kbd> denotes the origin point around which scale and rotation are applied. By default, it points to <kbd>Item.Center</kbd>, which makes the item scale and rotate around its center, but we can change it to eight other values, such as <kbd>Item.TopLeft</kbd> for the top-left corner or <kbd>Item.Right</kbd> for the middle of the right edge of the item. In the code we crafted, we rotate each item clockwise around its bottom edge. Each item is positioned horizontally in the middle of the plate using the <kbd>plate.width / 2</kbd> expression and vertically at the top of the plate with the default width of <kbd>0</kbd> and the height of half the plate's height; thus, each item is a thin vertical line spanning from within the top to the center of the plate. Then, each item is rotated around the center of the plate (each item's bottom edge) by 30 degrees more than a previous item effectively laying items evenly on the plate.</p>
<p>Finally, each item has a gray <kbd>Rectangle</kbd> attached to the top edge (offset by <kbd>4</kbd>) and horizontally centered in the transparent parent. Transformations applied to an item influence the item's children similar to what we have seen in Graphics View; thus, the effective rotation of the rectangle follows that of its parent. The height of the rectangle depends on the value of <kbd>hour</kbd>, which maps to the index of the item in <kbd>Repeater</kbd>. Here, you cannot use <kbd>index</kbd> directly as it is only visible within the topmost item of the delegate. That's why we create a real property called <kbd>hour</kbd> that can be referenced from within the whole delegate item hierarchy.</p>
<p>If you want more control over item transformations, then we are happy to inform you that apart from rotation and scale properties, each item can be assigned an array of elements such as <kbd>Rotation</kbd>, <kbd>Scale</kbd>, and <kbd>Translate</kbd> to a property called <kbd>transform</kbd>, which are applied in order, one at a time. These types have properties for fine-grained control over the transformation. For instance, using <kbd>Rotation</kbd>, you can implement rotation over any of the three axes and around a custom origin point (instead of being limited to nine predefined origin points as when using the <kbd>rotation</kbd> property of <kbd>Item</kbd>).</p>


            

            
        
    </div>



  
<div><h1 class="header-title">Time for action – Adding needles to the clock</h1>
                
            
            
                
<p>The next step is to add the hour, minute, and second needles to the clock. Let's start by creating a new component called <kbd>Needle</kbd> in a file called <kbd>Needle.qml</kbd> (remember that component names and files representing them need to start with a capital letter):</p>
<pre>import QtQuick 2.9

Rectangle {
    id: root

    property int value: 0
    property int granularity: 60
    property alias length: root.height

    width: 2
    height: parent.height / 2
    radius: width / 2
    antialiasing: true
    anchors.bottom: parent.verticalCenter
    anchors.horizontalCenter: parent.horizontalCenter
    transformOrigin: Item.Bottom
    rotation: 360 / granularity * (value % granularity)
} </pre>
<p><kbd>Needle</kbd> is basically a rectangle anchored to the center of its parent by its bottom edge, which is also the item's pivot. It also has the <kbd>value</kbd> and <kbd>granularity</kbd> properties driving the rotation of the item, where <kbd>value</kbd> is the current value the needle shows and <kbd>granularity</kbd> is the number of different values it can display. Also, anti-aliasing for the needle is enabled as we want the tip of the needle nicely rounded. Having such a definition, we can use the component to declare the three needles inside the clock plate object:</p>
<pre>Needle {
    length: plate.height * 0.3
    color: "blue"
    value: clock.hours
    granularity: 12
}
Needle {
    length: plate.height * 0.4
    color: "darkgreen"
    value: clock.minutes
    granularity: 60
}
Needle {
    width: 1
    length: plate.height * 0.45
    color: "red"
    value: clock.seconds
    granularity: 60
} </pre>
<p>The three needles make use of the <kbd>hours</kbd>, <kbd>minutes</kbd>, and <kbd>seconds</kbd> properties of clock, so these need to be declared as well:</p>
<pre>property int hours: 0
property int minutes: 0
property int seconds: 0 </pre>
<p>By assigning different values to the properties of <kbd>Clock</kbd> in <kbd>main.qml</kbd>, you can make the clock show a different time:</p>
<pre>import QtQuick 2.9

Clock {
    //...
    hours: 7
    minutes: 42
    seconds: 17
} </pre>
<p>You'll get an output as shown:</p>
<div><img src="img/349cc52a-3655-4afb-93e9-37089402cf32.png" style="width:14.17em;height:14.25em;" width="400" height="400"/></div>


            

            
        
    </div>



  
<div><h1 class="header-title">What just happened?</h1>
                
            
            
                
<p>Most <kbd>Needle</kbd> functionality is declared in the component itself, including geometry and transformations. Then, whenever we want to use the component, we declare an instance of <kbd>Needle</kbd> and optionally customize the <kbd>length</kbd> and <kbd>color</kbd> properties as well as set its <kbd>value</kbd> and <kbd>granularity</kbd> to obtain the exact functionality we need.</p>


            

            
        
    </div>



  
<div><h1 class="header-title">Time for action – Making the clock functional</h1>
                
            
            
                
<p>The final step in creating a clock is to make it actually show the current time. In JavaScript, we can query the current time using the <kbd>Date</kbd> object:</p>
<pre>var currentDate = new Date();
var hours   = currentDate.getHours();
var minutes = currentDate.getMinutes();
var seconds = currentDate.getSeconds();</pre>
<p>Therefore, the first thing that comes to mind is to use the preceding code to show the current time on the clock:</p>
<pre>Item {
    id: clock
    property int hours:   currentDate.getHours()
    property int minutes: currentDate.getMinutes()
    property int seconds: currentDate.getSeconds()
    property date currentDate: new Date()
    // ...
} </pre>
<p>This will indeed show the current time once you start the application, but the clock will not be updating itself as the time passes. This is because <kbd>new Date()</kbd> returns an object representing one particular moment in time (the date and time at the moment when the object was instantiated). While QML usually is capable of automatically updating a property when the value of the bound expression changes, it's unable to do so in this case. Even if QML was smart enough to see that the <kbd>new Date()</kbd> property always returns a different date, it doesn't know how often we want to update the value, and updating it as frequently as possible is generally a bad idea. Thus, we need a way to manually schedule periodic execution of an action.</p>
<p>To obtain this effect in QML, we can use a <kbd>Timer</kbd> element that is an equivalent of <kbd>QTimer</kbd> in C++ and lets us periodically execute some code. Let's modify the code to use a timer:</p>
<pre>Item {
    id: clock<br/>    //...
    property alias running: timer.running
    Timer {
        id: timer
        repeat: true
        interval: 500
        running: true
        onTriggered: clock.currentDate = new Date()
    }<br/>    //...
} </pre>


            

            
        
    </div>



  
<div><h1 class="header-title">What just happened?</h1>
                
            
            
                
<p>By setting the <kbd>interval</kbd> property, we ask the timer to emit the <kbd>triggered</kbd> signal every 500 ms, causing our <kbd>currentDate</kbd> property to be updated with a new <kbd>Date</kbd>  object representing the current time. The clock is also given the <kbd>running</kbd> property (pointing to its equivalent in the timer) that can control whether updates should be enabled. The timer's <kbd>repeat</kbd><br/>
property is set to <kbd>true</kbd>; otherwise, it will trigger just once.</p>
<p>To briefly sum up what you have learned so far, we can say that you know how to create hierarchies of objects by declaring their instances, and you also know how to program new types in separate files, making definitions available as components to be instantiated in other QML files. You can even use the <kbd>Repeater</kbd> element to declare a series of objects based on a common stencil.</p>


            

            
        
    </div>



  
<div><h1 class="header-title">Dynamic and lazy loading of QML objects</h1>
                
            
            
                
<p class="mce-root">All our previous QML projects contain an explicit declaration of the object tree. We usually create a window and place multiple specific elements into it in specific order. The QML engine creates these objects on startup and keeps them alive until the application terminates. This is a very convenient approach that allows you to save a lot of time, as you could see in our previous examples. However, sometimes you need the object tree to be more flexible—for example, if you don't know upfront which elements should be created. QML offers a few ways to create objects dynamically and to delay creating an object until you really need it. Utilizing these features can make your QML application more performant and flexible.</p>


            

            
        
    </div>



  
<div><h1 class="header-title">Creating objects on request</h1>
                
            
            
                
<p>The problem with predeclaring objects directly in a QML file is that you need to know upfront how many objects you will need. More often than not, you will want to dynamically add and remove objects to your scene, for example, in an alien invasion game, where, as the player progresses, new alien saucers will be entering the game screen and other saucers will be getting shot down and destroyed. Also, the player's ship will be "producing" new bullets streaking in front of the ship, eventually running out of fuel or otherwise disappearing from the game scene. By putting a good amount of effort into the problem, you will be able to use <kbd>Repeater</kbd> to obtain this effect, but there is a better tool at hand.</p>
<p>QML offers us another element type called <kbd>Component</kbd>, which is another way to teach the engine about a new element type by declaring its contents in QML. There are basically two approaches to doing this.</p>
<p>The first approach is to declare a <kbd>Component</kbd> element instance in the QML file and inline the definition of the new type directly inside the element:</p>
<pre>Component {
    id: circleComponent
    Item {
        //...
    }
} </pre>
<p>The other approach is to load the component definition from an existing QML file. Let's say that we have a <kbd>Circle.qml</kbd> file with the following content:</p>
<pre>import QtQuick 2.9<br/>Item {<br/>    property int diameter: 20<br/>    property alias color: rect.color<br/>    property alias border: rect.border<br/><br/>    implicitWidth: diameter<br/>    implicitHeight: diameter<br/><br/>    Rectangle {<br/>        id: rect<br/>        width: radius<br/>        height: radius<br/>        radius: diameter / 2<br/>        anchors.centerIn: parent<br/>    }<br/>}</pre>
<p>Such code declares a component that defines a circle and exposes its <kbd>diameter</kbd>, <kbd>color</kbd>, and <kbd>border</kbd> properties. Let's see how we can create instances of this component dynamically.</p>
<p>QML exposes a special global object called <kbd>Qt</kbd>, which provides a set of interesting methods. One of the methods allows the caller to create a component passing the URL of an existing QML document:</p>
<pre>var circleComponent = Qt.createComponent("Circle.qml"); </pre>
<p>An interesting note is that <kbd>createComponent</kbd> can not only accept a local file path but also a remote URL, and if it understands the network scheme (for example, <kbd>http</kbd>), it will download the document automatically. In this case, you have to remember that it takes time to do that, so the component may not be ready immediately after calling  <kbd>createComponent</kbd>. Since the current loading status is kept in the <kbd>status</kbd> property, you can connect to the <kbd>statusChanged</kbd> signal to be notified when this happens. A typical code path looks similar to the following:</p>
<pre>Window {<br/>    //...<br/>    Component.onCompleted: {<br/>        <strong>var circleComponent = Qt.createComponent("Circle.qml");</strong><br/><strong>        if(circleComponent.status === Component.Ready) {</strong><br/><strong>            addCircles(circleComponent);</strong><br/><strong>        } else {</strong><br/><strong>            circleComponent.statusChanged.connect(function() {</strong><br/><strong>                if(circleComponent.status === Component.Ready) {</strong><br/><strong>                    addCircles(circleComponent);</strong><br/><strong>                }</strong><br/><strong>            });</strong><br/><strong>        }</strong><br/>    }<br/>}</pre>
<p>In this example, we use the <kbd>Component.onCompleted</kbd> handler to run the code as soon as the window object is created. This handler is available in all Qt Quick items and is often used to perform initialization. You can also use any other signal handler here. For example, you can start loading the component when a button is pressed or a timer has expired.</p>
<p>The counterpart of the <kbd>completed()</kbd> signal of <kbd>Component</kbd> is <kbd>destruction()</kbd>. You can use the <kbd>Component.onDestruction</kbd> handler to perform actions such as saving the state of the object to persistent storage or otherwise cleaning the object up.</p>
<p>If the component definition is incorrect or the document cannot be retrieved, the status of the object will change to <kbd>Error</kbd>. In that case, you can make use of the <kbd>errorString()</kbd> method to see what the actual problem is:</p>
<pre>if(circleComponent.status === Component.Error) {
    console.warn(circleComponent.errorString());
} </pre>
<p>Once you are sure that the component is ready, you can finally start creating objects from it. For this, the component exposes a method called <kbd>createObject</kbd>. In its simplest form, it accepts an object that is to become the parent of the newly born instance (similar to widget constructors accepting a pointer to a parent widget) and returns the new object itself so that you can assign it to some variable. Then, you can start setting the object's properties:</p>
<pre>Window {<br/>    //...<br/>    ColumnLayout {<br/>        id: layout<br/>        anchors.fill: parent<br/>    }<br/>    function addCircles(circleComponent) {<br/>        ["red", "yellow", "green"].forEach(function(color) {<br/><strong>            var circle = circleComponent.createObject(layout);</strong><br/><strong>            circle.color = color;</strong><br/><strong>            circle.Layout.alignment = Qt.AlignCenter;</strong><br/>        });<br/>    }<br/>    //...<br/>}</pre>
<p>A more complex invocation lets us do both these operations (create the object and set its properties) in a single call by passing a second parameter to <kbd>createObject</kbd>:</p>
<pre>var circle = circleComponent.createObject(layout,
    { diameter: 20, color: 'red' });</pre>
<p>The second parameter is a JavaScript object whose properties are to be applied to the object being created. The advantage of the latter syntax is that all property values are applied to the object as one atomic operation and they won't trigger property change handlers (just like when the item is declared in a QML document) instead of a series of separate operations, each of which sets the value for a single property, possibly causing an avalanche of change handler invocations in the object.</p>
<p>After creation, the object becomes a first-class citizen of the scene, acting in the same way as items declared directly in the QML document. The only difference is that a dynamically created object can also be dynamically destructed by calling its <kbd>destroy()</kbd> method, which is similar to calling <kbd>delete</kbd> on C++ objects. When speaking of destroying dynamic items, we have to point out that when you assign a result of <kbd>createObject</kbd> to a variable (like <kbd>circle</kbd>, in our example) and that variable goes out of scope, the item will not be released and garbage collected as its parent still holds a reference to it, preventing it from being recycled.</p>
<p>We didn't mention this explicitly before, but we have already used inline component definitions earlier in this chapter when we introduced the <kbd>Repeater</kbd> element. The repeated item defined within the repeater is in fact not a real item, but a component definition that is instantiated as many times as needed by the repeater.</p>


            

            
        
    </div>



  
<div><h1 class="header-title">Delaying item creation</h1>
                
            
            
                
<p>Another recurring scenario is that you do know how many elements you will need, but the problem is that you cannot determine upfront what type of elements they will be. At some point during the lifetime of your application, you will learn that information and will be able to instantiate an object. Until you gain the knowledge about the given component, you will need some kind of item placeholder where you will later put the real item. You can, of course, write some code to use the <kbd>createObject()</kbd> functionality of the component, but this is cumbersome.</p>
<p>Fortunately, Qt Quick offers a nicer solution in the form of a <kbd>Loader</kbd> item. This item type is exactly what we described it to be—a temporary placeholder for a real item that will be loaded on demand from an existing component. You can put <kbd>Loader</kbd> in place of another item and when you need to create this item, one way is to set the URL of a component to the <kbd>source</kbd> property:</p>
<pre>Loader {
    id: loader
}
//...
onSomeSignal: loader.source = "Circle.qml"</pre>
<p>You can also directly attach a real component to the <kbd>sourceComponent</kbd> of a <kbd>Loader</kbd>:</p>
<pre>Loader {
    id: loader
    sourceComponent: shouldBeLoaded ? circleComponent : undefined
} </pre>
<p>Immediately afterward, the magic begins and an instance of the component appears in the loader. If the <kbd>Loader</kbd> object has its size set explicitly (for example, by anchoring or setting the width and height), then the item will be resized to the size of the loader. If an explicit size is not set, then <kbd>Loader</kbd> will instead be resized to the size of the loaded element once the component is instantiated. In the following code, the loader has its size set explicitly, so when its item is created, it will respect the anchors and sizes declared here:</p>
<pre>Loader {
    anchors {
        left: parent.left
        leftMargin: 0.2 * parent.width
        right: parent.right
        verticalCenter: parent.verticalCenter
    }
    height: 250
    source: "Circle.qml"
} </pre>


            

            
        
    </div>



  
<div><h1 class="header-title">Imperative painting on Canvas using JavaScript</h1>
                
            
            
                
<p>Declaring graphical items is nice and easy, but as programmers, we're more used to writing imperative code, and some things are easier expressed as an algorithm rather than as a description of the final result to be achieved. It is easy to use QML to encode a definition of a primitive shape such as a rectangle in a compact way—all we need is to mark the origin point of the rectangle, its width, height, and optionally, a color. Writing down a declarative definition of a complex shape consisting of many control points positioned in given absolute coordinates, possibly with an outline in some parts of it, maybe accompanied by an image or two, is still possible in a language such as QML; however, this will result in a much more verbose and much less readable definition. This is a case where using an imperative approach might prove more effective. HTML (being a declarative language) already exposes a proven imperative interface for drawing different primitives called a <kbd>Canvas</kbd> that is widely used in web applications. Fortunately, Qt Quick provides us with its own implementation of a <kbd>Canvas</kbd> interface similar to the one from the web by letting us instantiate <kbd>Canvas</kbd> items. Such items can be used to draw straight and curved lines, simple and complex shapes, and graphs and graphic images. It can also add text, colors, shadows, gradients, and patterns. It can even perform low-level pixel operations. Finally, the output may be saved as an image file or serialized to a URL usable as source for an <kbd>Image</kbd> item. There are many tutorials and papers available out there on using an HTML canvas, and they can usually be easily applied to a Qt Quick canvas as well (the reference manual even includes a list of aspects you need to pay attention to when porting HTML canvas applications to a Qt Quick Canvas), so here we will just give you the very basics of imperative drawing in Qt Quick.</p>
<p>Consider a game where the player's health is measured by the condition of his heart—the slower the beat, the healthier the player is. We will use this kind of visualization as our exercise in practicing painting using the <kbd>Canvas</kbd> element.</p>


            

            
        
    </div>



  
<div><h1 class="header-title">Time for action – Preparing Canvas for heartbeat visualization</h1>
                
            
            
                
<p>Let's start with simple things by creating an empty Qt Quick project. Add the following code to the <kbd>main.qml</kbd> file:</p>
<pre>Window {
    //...
    Canvas {
        id: canvas

        implicitWidth: 600
        implicitHeight: 300

        onPaint: {
            var ctx = canvas.getContext("2d");
            ctx.clearRect(0, 0, canvas.width, canvas.height);
            ctx.strokeRect(50, 50, 100, 100);
        }
    }
}</pre>
<p>When you run the project, you will see a window containing a rectangle:</p>
<div><img src="img/0b1553a2-ae63-4fcd-ac97-abd0e997ff27.png" style="width:20.42em;height:18.75em;" width="320" height="293"/></div>


            

            
        
    </div>



  
<div><h1 class="header-title">What just happened?</h1>
                
            
            
                
<p>In the preceding code, we created a basic boilerplate code for using a canvas. First, we created a <kbd>Canvas</kbd> instance with an implicit width and height set. There, we created a handler for the <kbd>paint</kbd> signal that is emitted whenever the canvas needs to be redrawn. The code placed there retrieves a context for the canvas, which can be thought of as an equivalent to the <kbd>QPainter</kbd> instance we used when drawing on Qt widgets. We inform the canvas that we want its 2D context, which gives us a way to draw in two dimensions. A 2D context is the only context currently present for the <kbd>Canvas</kbd> element, but you still have to identify it explicitly—similar to HTML. Having the context ready, we tell it to clear the whole area of the canvas. This is different from the widget world in which when the <kbd>paintEvent</kbd> handler was called, the widget was already cleared for us and everything had to be redrawn from scratch. With <kbd>Canvas</kbd>, it is different; the previous content is kept by default so that you can draw over it if you want. Since we want to start with a clean sheet, we call <kbd>clearRect()</kbd> on the context. Finally, we use the <kbd>strokeRect()</kbd> convenience method that draws a rectangle on the canvas.</p>


            

            
        
    </div>



  
<div><h1 class="header-title">Time for action - drawing a heartbeat</h1>
                
            
            
                
<p>We will extend our component now and implement its main functionality—drawing a heartbeat-like diagram.</p>
<p>Add the following property declarations to the <kbd>canvas</kbd> object:</p>
<pre>property int lineWidth: 2
property var points: []
property real arg: -Math.PI </pre>
<p>Inside the <kbd>Canvas</kbd> section, add a declaration for a timer that will trigger updates of the picture:</p>
<pre>Timer {
    interval: 10
    repeat: true
    running: true
    onTriggered: {
        canvas.arg += Math.PI / 180;
        while(canvas.arg &gt;= Math.PI) {
            canvas.arg -= 2 * Math.PI;
        }
    }
}</pre>
<p>Then again, inside the <kbd>Canvas</kbd> section, define the handler for when the value of <kbd>arg</kbd> is modified:</p>
<pre>onArgChanged: {
    points.push(func(arg));
    points = points.slice(-canvas.width);
    canvas.requestPaint();
}</pre>
<p>This handler uses a custom JavaScript function—<kbd>func()</kbd>. Place the implementation of the function just above the handler:</p>
<pre>function func(argument) {
    var a = (2 * Math.PI / 10);
    var b = 4 * Math.PI / 5;
    return Math.sin(20 * argument) * (
        Math.exp(-Math.pow(argument / a, 2)) +
        Math.exp(-Math.pow((argument - b) / a, 2)) +
        Math.exp(-Math.pow((argument + b) / a, 2))
    );
}</pre>
<p>Finally, modify the <kbd>onPaint</kbd> signal handler:</p>
<pre>onPaint: {
    var ctx = canvas.getContext("2d");
    ctx.reset();
    ctx.clearRect(0, 0, canvas.width, canvas.height);
    var pointsToDraw = points.slice(-canvas.width);
    ctx.translate(0, canvas.height / 2);
    ctx.beginPath();
    ctx.moveTo(0, -pointsToDraw[0] * canvas.height / 2);
    for(var i = 1; i &lt; pointsToDraw.length; i++) {
        ctx.lineTo(i, -pointsToDraw[i] * canvas.height / 2);
    }
    ctx.lineWidth = canvas.lineWidth;
    ctx.stroke();
}</pre>
<p>Then, you can run the code and see a heartbeat-like diagram appear on the canvas:</p>
<div><img src="img/e6e910ae-4a56-41bf-bad2-d87807ec3e5e.png" style="width:19.00em;height:8.50em;" width="600" height="267"/></div>


            

            
        
    </div>



  
<div><h1 class="header-title">What just happened?</h1>
                
            
            
                
<p>We added two kinds of properties to the element. By introducing <kbd>lineWidth</kbd>, we can manipulate the width of the line that visualizes the heartbeat. The <kbd>points</kbd> variable stores an array of already calculated function values. We initialize it to an empty array. The <kbd>arg</kbd> variable stores the function argument that was last evaluated. The argument of the function should be in the range from −π to +π; thus, we initialize <kbd>arg</kbd> to <kbd>-Math.PI</kbd>. Then, we add a timer that ticks in regular intervals, incrementing <kbd>arg</kbd> by 1° until it reaches +π, in which case it is reset to the initial value.</p>
<p>Changes to <kbd>arg</kbd> are intercepted in the handler we implement next. In there, we push a new item to the array of points. The value is calculated by the <kbd>func</kbd> function, which is quite complicated, but it is sufficient to say that it returns a value from within a range of −1 to +1. The oldest records are removed from the array of points using <kbd>Array.slice()</kbd> so that at most, the last <kbd>canvas.width</kbd> items remain in the array. This is so that we can plot one point for each pixel of the width of the canvas, and we don't have to store any more data than required. At the end of the function, we invoke <kbd>requestPaint()</kbd>, which is an equivalent of <kbd>QWidget::update()</kbd> and schedules a repaint of the canvas.</p>
<p>That, in turn, calls our <kbd>onPaint</kbd> signal handler. There, after retrieving the context, we reset the canvas to its initial state and then calculate an array of points that is to be drawn again using <kbd>slice()</kbd>. Then, we prepare the canvas by translating and scaling it in the vertical axis so that the origin is moved to half of the height of the canvas (that's the reason for calling <kbd>reset()</kbd> at the beginning of the procedure—to revert this transformation). After that, <kbd>beginPath()</kbd> is called to inform the context that we are starting to build a new path. Then, the path is built segment by segment by appending lines. Each value is multiplied by <kbd>canvas.height / 2</kbd> so that values from the point array are scaled to the size of the item. The value is negated as the vertical axis of the canvas grows to the bottom, and we want positive values to be above the origin line. After that, we set the width of the pen and draw the path by calling <kbd>stroke()</kbd>.</p>


            

            
        
    </div>



  
<div><h1 class="header-title">Time for action – Hiding properties</h1>
                
            
            
                
<p>If we convert our heartbeat canvas to a QML component, the <kbd>points</kbd> and <kbd>arg</kbd> properties will be the public properties visible to the user of the component. However, they are really implementation details we want to hide. We should only expose properties that make sense to the user of the component, such as <kbd>lineWidth</kbd> or <kbd>color</kbd>.</p>
<p>Since the <kbd>Timer</kbd> object inside the <kbd>Canvas</kbd> is not exported as public property, that timer object will be unavailable from the outside, so we can attach properties to the timer instead of attaching them to the top-level <kbd>Canvas</kbd> object. However, the properties do not belong to the timer logically, so this solution will be confusing. For such cases, there is a convention that you should create an empty <kbd>QtObject</kbd> child in the top-level object and move properties into it:</p>
<pre>Canvas {
    id: canvas
    property int lineWidth: 2
    //...
    QtObject {
        id: d
        property var points: []
        property real arg: -Math.PI

        function func(argument) { /* ... */ }
        onArgChanged: { /* ... */ }
    }
    //...
}</pre>
<p><kbd>QtObject</kbd> is the QML representation of the <kbd>QObject</kbd> class. It is a QML type that doesn't have any particular functionality, but can hold properties. As part of the convention, we set <kbd>id</kbd> of this object to <kbd>d</kbd>. The <kbd>onArgChanged</kbd> handler is moved to the private object as well. In the <kbd>onTriggered</kbd> and <kbd>onPaint</kbd> handlers, we should now refer to the internal properties as <kbd>d.points</kbd> and <kbd>d.arg</kbd>. Consider this example:</p>
<pre>onTriggered: {
    d.arg += Math.PI / 180;
    while(d.arg &gt;= Math.PI) {
        d.arg -= 2 * Math.PI;
    }
}</pre>
<p>The <kbd>points</kbd> and <kbd>arg</kbd> properties are now unavailable from the outside, leading to clean public interface of our heartbeat object.</p>


            

            
        
    </div>



  
<div><h1 class="header-title">Time for action – Making the diagram more colorful</h1>
                
            
            
                
<p>The diagram serves its purpose, but it looks a bit dull. Add some shine to it by defining three new color properties in the canvas object—<kbd>color</kbd>, <kbd>topColor</kbd>, <kbd>bottomColor</kbd>—and setting their default values to <kbd>black</kbd>, <kbd>red</kbd>, and <kbd>blue</kbd>, respectively:</p>
<pre>property color color: "black"
property color topColor: "red"
property color bottomColor: "blue"</pre>
<p>Then, let's make use of these properties by extending <kbd>onPaint</kbd> implementation:</p>
<pre>onPaint: {
    //...
    // fill:
    ctx.beginPath();
    ctx.moveTo(0, 0);
    var i;
    for(i = 0; i &lt; pointsToDraw.length; i++) {
        ctx.lineTo(i, -pointsToDraw[i] * canvas.height/2);
    }
    ctx.lineTo(i, 0);
    var gradient = ctx.createLinearGradient(
            0, -canvas.height / 2, 0, canvas.height / 2);
    gradient.addColorStop(0.1, canvas.topColor);
    gradient.addColorStop(0.5, Qt.rgba(1, 1, 1, 0));
    gradient.addColorStop(0.9, canvas.bottomColor);
    ctx.fillStyle = gradient;
    ctx.fill();

    // stroke:
    ctx.beginPath();
    ctx.moveTo(0, -pointsToDraw[0] * canvas.height / 2);
    for(i = 1; i &lt; pointsToDraw.length; i++) {
        ctx.lineTo(i, -pointsToDraw[i] * canvas.height / 2);
    }
    ctx.lineWidth = canvas.lineWidth;
    ctx.strokeStyle = canvas.color;
    ctx.stroke();
}</pre>
<p>Upon running the preceding code snippet, you get the following output:</p>
<div><img src="img/a9ce7759-007e-440c-9c3c-a6df4b2b1b81.png" style="width:25.83em;height:11.92em;" width="682" height="313"/></div>


            

            
        
    </div>



  
<div><h1 class="header-title">What just happened?</h1>
                
            
            
                
<p>The modifications to <kbd>onPaint</kbd> that we implemented are creating another path and using that path to fill an area using a gradient. The path is very similar to the original one, but it contains two additional points that are the first and last points drawn projected onto the horizontal axis. This ensures that the gradient fills the area properly. Note that the canvas uses imperative code for drawing; therefore, the order of drawing the fill and the stroke matters—the fill has to be drawn first so that it doesn't obscure the stroke.</p>


            

            
        
    </div>



  
<div><h1 class="header-title">Using C++ classes as QML components</h1>
                
            
            
                
<p>In the next exercise, we will implement a car dashboard that can be used in a racing game and will show a number of parameters such as current speed and motor revolutions per minute. The input data will be provided by a C++ object. We'll see how to include this object into the QML object tree and use property bindings to implement the dashboard.</p>
<p>The final result will look similar to the following:</p>
<div><img src="img/a197c586-48c3-456d-b365-297c88d58738.png" width="1714" height="898"/></div>


            

            
        
    </div>



  
<div><h1 class="header-title">Time for action – Self-updating car dashboard</h1>
                
            
            
                
<p>We will start with the C++ part. Set up a new Qt Quick application. This will generate the main function for you that instantiates <kbd>QGuiApplication</kbd>   and  <kbd>QQmlApplicationEngine</kbd> and sets them up to load a QML document.</p>
<p>Use the File menu to create New file or Project and create a new Qt Designer form class. Call it <kbd>CarInfo</kbd> and choose the <kbd>Widget</kbd> template. We will use this class for modifying values of different parameters so that we may observe how they influence what the Qt Quick scene displays. In the class declaration, add the following properties:</p>
<pre>class CarInfo : public QWidget {
    Q_OBJECT
    Q_PROPERTY(int rpm READ rpm NOTIFY rpmChanged)
    Q_PROPERTY(int gear READ gear NOTIFY gearChanged)
    Q_PROPERTY(int speed READ speed NOTIFY speedChanged)
    Q_PROPERTY(double distance READ distance NOTIFY distanceChanged)
    //...
};</pre>
<p>The properties are read-only, and the <kbd>NOTIFY</kbd> clause defines signals emitted when the respective property values change. Go ahead and implement the appropriate functions for each property. Apart from the getter, also implement a setter as a public slot. Here's an example for a property controlling the speed of the car:</p>
<pre>int CarInfo::speed() const {
    return m_speed;
}
void CarInfo::setSpeed(int newSpeed) {
    if(m_speed == newSpeed) {
        return;
    }
    m_speed = newSpeed;
    emit speedChanged(m_speed);
} </pre>
<p>You should be able to follow the example for the remaining properties on your own.</p>
<p>Since we want to use the widget to tweak property values, design the user interface for it using the form editor. It can look like this:</p>
<div><img src="img/b5191e3b-b1a6-41fd-9a29-5e297308d7c9.png" style="width:26.50em;height:12.92em;" width="379" height="184"/></div>
<p>Make appropriate signal-slot connections in the widget so that modifying any of the widgets for a given parameter or using the setter slot directly updates all the widgets for that parameter.</p>
<p>Instead of adding member variables to the <kbd>CarInfo</kbd> class for properties such as <kbd>speed</kbd>, <kbd>rpm</kbd>, <kbd>distance</kbd>, or <kbd>gear</kbd>, you can operate directly on the widgets placed on the <kbd>ui</kbd> form, as shown further.</p>
<p>For example, a getter for the <kbd>distance</kbd> property will look like this:</p>
<pre>qreal CarInfo::distance() const
{
    return ui-&gt;distanceBox-&gt;value();
}</pre>
<p>The setter would then be modified to the following:</p>
<pre>void CarInfo::setDistance(qreal newDistance)
{
    ui-&gt;distanceBox-&gt;setValue(newDistance);
}</pre>
<p>You will then need to add <kbd>connect()</kbd> statements to the constructor to ensure that signals are propagated from the ui form:</p>
<pre>connect(ui-&gt;distanceBox, SIGNAL(valueChanged(double)),
        this,            SIGNAL(distanceChanged(double)));   </pre>
<p>Next, you can test your work by running the widget. To do this, you have to alter the main function to look as follows:</p>
<pre>int main(int argc, char **argv) {
    QApplication app(argc, argv);
    CarInfo cinfo;
    cinfo.show();
    return app.exec();
}; </pre>
<p>Since we are using widgets, we have to replace <kbd>QGuiApplication</kbd> with  <kbd>QApplication</kbd>  and enable the widgets module by placing <kbd>QT += widgets</kbd> in the project file (remember to run <kbd>qmake</kbd> from the project's context menu afterward). Ensure that everything works as expected (that is, that moving sliders and changing spinbox values reflect the changes to widget properties) before moving on to the next step.</p>
<p>We will now add Qt Quick to the equation, so let's start by updating our main function to display our scene. Introduce the highlighted changes to the code:</p>
<pre>int main(int argc, char **argv) {
    QApplication app(argc, argv);
    CarInfo cinfo;
    <strong>QQmlApplicationEngine engine;
    engine.rootContext()-&gt;setContextProperty("carData", &amp;cinfo);
    engine.load(QUrl(QStringLiteral("qrc:/main.qml")));
    if (engine.rootObjects().isEmpty())
        return -1;</strong>
    cinfo.show();
    return app.exec();
}</pre>
<p>The modifications create a QML engine for our scene, export the <kbd>CarInfo</kbd> instance to the global context of the QML engine, and load and display the scene from a file located in a resource.</p>
<p>It is important to first export all the objects and only then load the scene. This is because we want all the names to be already resolvable when the scene is being initialized so that they can be used right away. If we reversed the order of calls, we would get a number of warnings on the console about the identities being undefined.</p>
<p>Finally, we can focus on the QML part. Look at the picture of the result we want to be shown at the beginning of the exercise. For the black background, we used a bitmap image created in a graphical editor (you can find the file in the materials for this book), but you can obtain a similar effect by composing three black rounded rectangles directly in Qt Quick—the two outer parts are perfect circles, and the inner module is a horizontally stretched ellipse.</p>
<p>If you decide to use our background file (or make your own prettier image), you should add it to the project's resources and put the following code into <kbd>main.qml</kbd>:</p>
<pre>import QtQuick 2.9
import QtQuick.Window 2.3

Window {
    visible: true
    width: backgroundImage.width
    height: backgroundImage.height

    Image {
        id: backgroundImage
        source: "qrc:/dashboard.png"
        Item {
            id: leftContainer
            anchors.centerIn: parent
            anchors.horizontalCenterOffset: -550
            width: 400; height: width
        }
        Item {
            id: middleContainer
            anchors.centerIn: parent
            width: 700; height: width
        }
        Item {
            id: rightContainer
            anchors.centerIn: parent
            anchors.horizontalCenterOffset: 525
            width: 400; height: width
        }
    }
}</pre>
<p>What we do here is add the image to the window and create three items to serve as containers for different elements of the dashboard. The containers are all centered in the parent, and we use a <kbd>horizontalCenterOffset</kbd> property to move the two outer items sideways. The values of the offset as well as the widths are based on the background image's geometry (note that all three containers are perfect squares). If instead of using our file, you settle for creating the three parts yourself using Qt Quick items, the containers will simply be anchored to the centers of the three black items.</p>
<p>The dials look complicated, but in reality, they are very easy to implement, and you have already learned everything you need to design them.</p>
<p>Let's start with the needle. Use the context menu of the resource file to create a new QML file and call it <kbd>Needle.qml</kbd>. Open the file and place the following content:</p>
<pre>import QtQuick 2.9

Item {
    id: root
    property int length: parent.width * 0.4
    property color color: "white"
    property color middleColor: "red"
    property int size: 2

    Rectangle { //needle
        width: root.size
        height: length + 20
        color: root.color
        anchors.horizontalCenter: parent.horizontalCenter
        anchors.bottom: parent.bottom
        anchors.bottomMargin: -20
        antialiasing: true
    }

    Rectangle { //fixing
        anchors.centerIn: parent
        width: 8 + root.size
        height: width
        radius: width / 2
        color: root.color
        Rectangle { //middle dot
            anchors {
                fill: parent
                margins: parent.width * 0.25
            }
            color: root.middleColor
        }
    }
}</pre>
<p>The document defines an item with four attributes—the length of the needle (defaults to 80% of the dial's radius), the color of the needle, <kbd>middleColor</kbd>, which stands for the color of the needle's fixing, and the size, which defines how wide the needle is. The code is self-explanatory. The item itself does not have any dimensions and only acts as an anchor for visual elements—the needle itself is a thin rectangle oriented vertically with a fixing 20 units from the end. The fixing is a circle of the same color as the needle with a smaller circle in the middle that uses a different fill color. The smaller radius of the inner circle is obtained by filling the outer circle with a 25% margin from each side.</p>
<p>As for the dials, we will put their code inline in the main file since we just have two of them and they differ a bit, so the overhead of creating a separate component with a well-designed set of properties will outweigh the benefits of having nicely encapsulated objects.</p>
<p>If you think about what needs to be done to have the dial displayed and working, it seems that the hardest thing is to lay out the numbers nicely on the circle, so let's start with that. Here's an implementation of a function for calculating the position along a circle circumference, based on the radius of the circle and angle (in degrees) where an item should be positioned:</p>
<pre>function calculatePosition(angle, radius) {<br/>    if(radius === undefined) {<br/>        radius = width / 2 * 0.8;<br/>    }<br/>    var a = angle * Math.PI / 180;<br/>    var px = width / 2 + radius * Math.cos(a);<br/>    var py = width / 2 + radius * Math.sin(a);<br/>    return Qt.point(px, py);<br/>}</pre>
<p>The function converts degrees to radians and returns the desired point. The function expects the <kbd>width</kbd> property to be available that helps calculate the center of the circle and in case a radius was not given, serves as a means to calculate a feasible value for it.</p>
<p>With such a function available, we can use the already familiar <kbd>Repeater</kbd> element to position items where we want them. Let's put the function in <kbd>middleContainer</kbd> and declare the dial for car speed:</p>
<pre>Item {
    id: middleContainer
    // ...
    function calculatePosition(angle, radius) { /* ... */ }
    Repeater {
        model: 24 / 2
        Item {
            property point pt:
            middleContainer.calculatePosition(120 + index * 12 * 2)
            x: pt.x<br/>            y: pt.y
            Label {
                anchors.centerIn: parent
                text: index * 20
            }
        }
    }
    Needle {
        anchors.centerIn: parent
        length: parent.width * 0.35
        size: 4
        rotation: 210 + (carData.speed * 12 / 10)
        color: "yellow"
    }
} </pre>
<p>You might have noted that we used an element called <kbd>Label</kbd>. We created it to avoid having to set the same property values for all the texts we use in the user interface:</p>
<pre>import QtQuick 2.9

Text {
    color: "white"
    font.pixelSize: 24
} </pre>
<p>The dial consists of a repeater that will create 12 elements. Each element is an item positioned using the earlier described function. The item has a label anchored to it that displays the given speed. We use <kbd>120 + index * 12 * 2</kbd> as the angle expression as we want "0" to be positioned at 120 degrees and each following item positioned 24 degrees further.</p>
<p>The needle is given rotation based on the value read from the <kbd>carData</kbd> object. Since the angular distance between consecutive 20 kph labels is 24 degrees, the distance for one kph is 1.2 and thus we multiply <kbd>carData.speed</kbd> by that factor. Item rotation is calculated with 0 degrees "pointing right"; therefore, we add 90 to the initial 120 degree offset of the first label to obtain starting coordinates matching those of the label system.</p>
<p>As you can see in the image of the final result at the beginning of this section, the speed dial contains small lines every 2 kph, with those divisible by 10 kph longer than others. We can use another <kbd>Repeater</kbd> to declare such ticks:</p>
<pre>Repeater {
    model: 120 - 4

    Item {
        property point pt: middleContainer.calculatePosition(
            120 + index * 1.2 * 2, middleContainer.width * 0.35
        )
        x: pt.x<br/>        y: pt.y
        Rectangle {
            width: 2
            height: index % 5 ? 5 : 10
            color: "white"
            rotation: 210 + index * 1.2 * 2
            anchors.centerIn: parent
            antialiasing: true
        }
    }
} </pre>
<p>Finally, we can put a label for the dial:</p>
<pre>Text {
    anchors.centerIn: parent
    anchors.verticalCenterOffset: 40
    text: "SPEED\n[kph]"
    horizontalAlignment: Text.AlignHCenter
    color: "#aaa"
    font.pixelSize: 16
} </pre>
<p>Ensure that the label is declared before the dial needle, or give the needle a higher <em>z</em> value so that the label doesn't overpaint the needle.</p>
<p>Next, repeat the process on your own for the left container by creating an RPM dial reading values from the <kbd>carData.rpm</kbd> property. The dial also displays the current gear of the car's engine. Place the following code inside the <kbd>leftContainer</kbd> object definition:</p>
<pre>Item {<br/>    id: gearContainer<br/>    anchors.centerIn: parent<br/>    anchors.horizontalCenterOffset: 10<br/>    anchors.verticalCenterOffset: -10<br/><br/>    Text {<br/>        id: gear<br/>        property int value: carData.gear<br/>        <strong>property var gears: [</strong><br/><strong>            "R", "N",</strong><br/><strong>            "1&lt;sup&gt;st&lt;/sup&gt;", "2&lt;sup&gt;nd&lt;/sup&gt;", "3&lt;sup&gt;rd&lt;/sup&gt;",</strong><br/><strong>            "4&lt;sup&gt;th&lt;/sup&gt;", "5&lt;sup&gt;th&lt;/sup&gt;"</strong><br/><strong>        ]</strong><br/><strong>        text: gears[value + 1]</strong><br/>        anchors.left: parent.left<br/>        anchors.bottom: parent.bottom<br/>        color: "yellow"<br/>        font.pixelSize: 32<br/>        textFormat: Text.RichText<br/>    }<br/>} </pre>
<p>The only part needing explanation is highlighted. It defines an array of gear labels starting with reverse, going through neutral, and then through five forward gears. The array is then indexed with the current gear and the text for that value is applied to the label. Note that the value is incremented by 1, which means the 0th index of the array will be used when <kbd>carData.gear</kbd> is set to <kbd>1</kbd>.</p>
<p>We will not show how to implement the right container. You can do that easily yourself now with the use of the <kbd>Grid</kbd> positioner to lay out the labels and their values. To display the series of controls on the bottom of the right container (with texts <kbd>ABS</kbd>, <kbd>ESP</kbd>, <kbd>BRK</kbd>, and <kbd>CHECK</kbd>), you can use <kbd>Row</kbd> of <kbd>Label</kbd> instances.</p>
<p>Now, start the program and begin moving the sliders on the widget. See how the Qt Quick scene follows the changes.</p>


            

            
        
    </div>



  
<div><h1 class="header-title">What just happened?</h1>
                
            
            
                
<p>We have created a very simple <kbd>QObject</kbd> instance and exposed it as our "data model" to QML. The object has a number of properties that can receive different values. Changing a value results in emitting a signal, which in turn notifies the QML engine and causes bindings containing those properties to be reevaluated. As a result, our user interface gets updated.</p>


            

            
        
    </div>



  
<div><h1 class="header-title">Time for action – Grouping engine properties</h1>
                
            
            
                
<p>The data interface between the QML and C++ worlds that we created is very simple and has a small number of properties. However, as the amount of data we want to expose grows, the object can become cluttered. Of course, we can counter that effect by dividing it into multiple smaller objects, each having separate responsibilities and then exporting all those objects to QML, but that is not always desirable. In our case, we can see that <kbd>rpm</kbd> and <kbd>gear</kbd> are properties of the engine subsystem, so we can move them to a separate object; however, in reality, their values are tightly coupled with the speed of the car and to calculate the speed, we will need to know the values of those two parameters. However, the speed also depends on other factors such as the slope of the road, so putting the speed into the engine subsystem object just doesn't seem right. Fortunately, there is a nice solution to that problem.</p>
<p>QML has a concept called grouped properties. You already know a number of them—the <kbd>border</kbd> property of the <kbd>Rectangle</kbd> element or the <kbd>anchors</kbd> property of the <kbd>Item</kbd> element, for example. Let's see how to define such properties for our exposed object.</p>
<p>Create a new <kbd>QObject</kbd>-derived class and call it <kbd>CarInfoEngine</kbd>. Move the property definitions of <kbd>rpm</kbd> and <kbd>gear</kbd> to that new class along with their getters, setters, and change signals. Add the following property declaration to <kbd>CarInfo</kbd>:</p>
<pre>Q_PROPERTY(QObject* engine READ engine NOTIFY engineChanged) </pre>
<p>Implement the getter and the private field:</p>
<pre>    QObject* engine() const { return m_engine; }
private:
    CarInfoEngine *m_engine; </pre>
<p>We will not use the signal right now. However, we had to declare it; otherwise, QML would complain we were binding expressions that depend on properties that are non-notifiable:</p>
<pre>signals:
    void engineChanged(); </pre>
<p>Initialize <kbd>m_engine</kbd> in the constructor of <kbd>CarInfo</kbd>:</p>
<pre>m_engine = new CarInfoEngine(this); </pre>
<p>Next, update the code of <kbd>CarInfo</kbd> to modify properties of <kbd>m_engine</kbd> whenever respective sliders on the widget are moved. Provide a link the other way as well—if the property value is changed, update the user interface accordingly.</p>
<p>Update the QML document and replace <kbd>carData.gear</kbd> with <kbd>carData.engine.gear</kbd>. Do the same for <kbd>carData.rpm</kbd> and <kbd>carData.engine.rpm</kbd>. You should end up with something along the lines of the following:</p>
<pre>Item {<br/>    id: leftContainer<br/>    // ...<br/><br/>    Item {<br/>        id: gearContainer<br/>        Text {<br/>            id: gear<br/>            property int value: carData.engine.gear<br/>            // ...<br/>        }<br/>    }<br/>    Needle {<br/>        anchors.centerIn: parent<br/>        length: parent.width * 0.35<br/>        rotation: 210 + (carData.engine.rpm * 35)<br/>    }<br/>} </pre>


            

            
        
    </div>



  
<div><h1 class="header-title">What just happened?</h1>
                
            
            
                
<p>Essentially, what we did is expose a property in <kbd>CarInfo</kbd> that is itself an object that exposes a set of properties. This object of the <kbd>CarInfoEngine</kbd> type is bound to the <kbd>CarInfo</kbd> instance it refers to.</p>


            

            
        
    </div>



  
<div><h1 class="header-title">Time for action – Registering C++ class as QML type</h1>
                
            
            
                
<p>So far, what we did was expose ourselves to QML single objects created and initialized in C++. However, we can do much more—the framework allows us to define new QML types. These can either be generic <kbd>QObject</kbd>-derived QML elements or items specialized for Qt Quick.</p>
<p>We will start with something simple—exposing the <kbd>CarInfo</kbd> type to QML so that instead of instantiating it in C++ and then exposing it in QML, we can directly declare the element in QML and still allow the changes made to the widget to be reflected in the scene.</p>
<p>To make a certain class (derived from <kbd>QObject</kbd>) instantiable in QML, all that is required is to register that class with the declarative engine using the <kbd>qmlRegisterType</kbd> template function. This function takes the class as its template parameter along a number of function arguments: the module <kbd>uri</kbd>, the major and minor version numbers, and the name of the QML type we are registering. The following call will register the <kbd>FooClass</kbd> class as the QML type <kbd>Foo</kbd>, available after importing <kbd>foo.bar.baz</kbd> in Version 1.0:</p>
<pre>qmlRegisterType&lt;FooClass&gt;("foo.bar.baz", 1, 0, "Foo"); </pre>
<p>You can place this invocation anywhere in your C++ code; just ensure that this is before you try to load a QML document that might contain declarations of <kbd>Foo</kbd> objects. A typical place to put the function call is in the program's main function. Afterward, you can start declaring objects of the <kbd>Foo</kbd> type in your documents. Just remember that you have to import the respective module first:</p>
<pre>import QtQuick 2.9
<strong>import foo.bar.baz 1.0</strong>

Item {
    <strong>Foo</strong> {
        id: foo
    }
} </pre>


            

            
        
    </div>



  
<div><h1 class="header-title">Time for action – Making CarInfo instantiable from QML</h1>
                
            
            
                
<p>First, we will update the QML document to create an instance of <kbd>CarInfo</kbd> present in the <kbd>CarInfo 1.0</kbd> module:</p>
<pre>import QtQuick 2.9
import CarInfo 1.0

Image {
    source: "dashboard.png"

    CarInfo {
        id: carData
        visible: true // make the widget visible
    }
  // ...
} </pre>
<p>As for registering <kbd>CarInfo</kbd>, it might be tempting to simply call <kbd>qmlRegisterType</kbd> on <kbd>CarInfo</kbd> and congratulate ourselves for a job well done:</p>
<pre>int main(int argc, char **argv) {
    QGuiApplication app(argc, argv);<br/>    QQmlApplicationEngine engine;<br/>    // this code does not work
    <strong>qmlRegisterType&lt;CarInfo&gt;("CarInfo", 1, 0, "CarInfo");</strong><br/>    //...
} </pre>
<p>In general, this would work (yes, it is as simple as that). However, it will not work with widgets. It's not possible to include <kbd>QWidget</kbd>-based objects into a QML object tree because a <kbd>QWidget</kbd> object can only have another <kbd>QWidget</kbd> object as its parent, and QML needs to set the outer QML object as the parent. To resolve this conflict, we need to ensure that what we instantiate is not a widget. For that, we will use a proxy object that will forward our calls to the actual widget. Therefore, create a new class called <kbd>CarInfoProxy</kbd> derived from <kbd>QObject</kbd> and make it have the same properties as <kbd>CarInfo</kbd>. Consider this example:</p>
<pre>class CarInfoProxy : public QObject {
    Q_OBJECT
    Q_PROPERTY(QObject *engine READ engine NOTIFY engineChanged)
    Q_PROPERTY(int speed READ speed WRITE setSpeed NOTIFY speedChanged)
    // ... </pre>
<p>Declare one more property that will let us show and hide the widget on demand:</p>
<pre>  Q_PROPERTY(bool visible READ visible WRITE setVisible <br/>                          NOTIFY visibleChanged)</pre>
<p>Then, we can place the widget as a member variable of the proxy so that it is created and destroyed alongside its proxy:</p>
<pre>private:
    CarInfo m_car; </pre>
<p>This way, the <kbd>CarInfo</kbd> widget will have <kbd>nullptr</kbd> parent, so it will be displayed as a top-level window. The QML engine will create an object of the <kbd>CarInfoProxy</kbd> class and set its parent to be another QML object, but this will not affect the parent of the widget.</p>
<p>Next, implement the missing interface. For simplicity, we are showing you code for some of the properties. The others are similar, so you can fill in the gaps on your own:</p>
<pre>public:<br/>    CarInfoProxy(QObject *parent = nullptr) : QObject(parent) {<br/>        connect(&amp;m_car, &amp;CarInfo::engineChanged,<br/>                this, &amp;CarInfoProxy::engineChanged);<br/>        connect(&amp;m_car, &amp;CarInfo::speedChanged,<br/>                this, &amp;CarInfoProxy::speedChanged);<br/>    }<br/>    QObject *engine() const { <br/>        return m_car.engine(); <br/>    }<br/>    bool visible() const { <br/>        return m_car.isVisible(); <br/>    }<br/>    void setVisible(bool v) {<br/>        if(v == visible()) return;<br/>        m_car.setVisible(v);<br/>        emit visibleChanged(v);<br/>    }<br/>    int speed() const { <br/>        return m_car.speed(); <br/>    }<br/>    void setSpeed(int v) { <br/>        m_car.setSpeed(v); <br/>    }<br/>signals:<br/>    void engineChanged();<br/>    void visibleChanged(bool);<br/>    void speedChanged(int);<br/>}; </pre>
<p>You can see that we reuse the <kbd>CarInfoEngine</kbd> instance from the widget instead of duplicating it in the proxy class. Finally, we can register <kbd>CarInfoProxy</kbd> as <kbd>CarInfo</kbd>:</p>
<pre>  qmlRegisterType&lt;CarInfoProxy&gt;("CarInfo", 1, 0, "CarInfo"); </pre>
<p>If you run the code now, you will see that it works—<kbd>CarInfo</kbd> has become a regular QML element. Due to this, its properties can be set and modified directly in the document, right? If you try setting the speed or the distance, it will work just fine. However, try to set a property grouped in the <kbd>engine</kbd> property:</p>
<pre>CarInfo {<br/>    id: carData<br/>    visible: true<br/><strong>    engine.gear: 3</strong><br/>}</pre>
<p>QML runtime will complain with a message similar to the following one:</p>
<pre>Cannot assign to non-existent property "gear"
             engine.gear: 3
                    ^ </pre>
<p>This is because the runtime does not understand the <kbd>engine</kbd> property—we declared it as <kbd>QObject</kbd> and yet we are using a property this class doesn't have. To avoid this issue, we have to teach the runtime about <kbd>CarInfoEngine</kbd>.</p>
<p>First, let's update the property declaration macro to use <kbd>CarInfoEngine</kbd> instead of <kbd>QObject</kbd>:</p>
<pre>Q_PROPERTY(CarInfoEngine* engine READ engine NOTIFY engineChanged) </pre>
<p>Also, the getter function itself:</p>
<pre>CarInfoEngine* engine() const { <br/>    return m_engine; <br/>}</pre>
<p>You should make these changes in both the <kbd>CarInfo</kbd> and <kbd>CarInfoProxy</kbd> classes. Then, we should teach the runtime about the type:</p>
<pre>QString msg = QStringLiteral("Objects of type CarInfoEngine cannot be created");<br/>qmlRegisterUncreatableType&lt;CarInfoEngine&gt;("CarInfo", 1, 0, "CarInfoEngine", msg);</pre>


            

            
        
    </div>



  
<div><h1 class="header-title">What just happened?</h1>
                
            
            
                
<p>In this exercise, we let the QML runtime know about two new elements. One of them is <kbd>CarInfo</kbd>, which is a proxy to our widget class. We told the engine that this is a full-featured class that is instantiable from QML. The other class, <kbd>CarInfoEngine</kbd>, also became known to QML; however, the difference is that every attempt to declare an object of this type in QML fails with a given warning message. There are other functions available for registering types in QML, but they are rarely used, so we will not be describing them here. If you are curious about them, type in <kbd>qmlRegister</kbd> in the Index tab of Creator's Help pane.</p>


            

            
        
    </div>



  
<div><h1 class="header-title">Pop quiz</h1>
                
            
            
                
<p>Q1. Which QML type allows you to create a placeholder for an object that will be instantiated later?</p>
<ol>
<li class="mce-root"><kbd>Repeater</kbd></li>
<li class="mce-root"><kbd>Loader</kbd></li>
<li class="mce-root"><kbd>Component</kbd></li>
</ol>
<p class="mce-root">Q2. Which QML type provides low-level access to individual touch events?</p>
<ol>
<li class="mce-root"><kbd>PinchArea</kbd></li>
<li class="mce-root"><kbd>MouseArea</kbd></li>
<li class="mce-root"><kbd>MultiPointTouchArea</kbd></li>
</ol>
<p class="mce-root">Q3. When can you access a component defined in another QML file without an import statement?</p>
<ol>
<li class="mce-root">You can do that if the component is registered using the <kbd>qmlRegisterType</kbd> function</li>
<li class="mce-root">You can do that if the component file is added to the project resources</li>
<li class="mce-root">You can do that if the component file is in the same directory as the current file</li>
</ol>


            

            
        
    </div>



  
<div><h1 class="header-title">Summary</h1>
                
            
            
                
<p>You are now familiar with multiple methods that can be used to extend Qt Quick with your own item types. You learned to use JavaScript to create custom visual items. You also know how to use C++ classes as non-visual QML elements fully integrated with your UI. We also discussed how to handle mouse, touch, keyboard, and gamepad events in Qt Quick applications. However, so far, despite us talking about "fluid" and "dynamic" interfaces, you haven't seen much of them. Do not worry; in the next chapter, we will focus on animations in Qt Quick as well as fancy graphics and applying what you learned in this chapter for creating nice-looking and interesting games. So, read on!</p>


            

            
        
    </div>



  </body></html>